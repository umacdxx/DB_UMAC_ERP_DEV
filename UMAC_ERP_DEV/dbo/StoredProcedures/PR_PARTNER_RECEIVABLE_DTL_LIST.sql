/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.06.17
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 미수 처리 내역(미완)
-- 실행문 : 
EXEC PR_PARTNER_RECEIVABLE_DTL_LIST 'UM20128','20240601','20240704'
*/
CREATE PROCEDURE [dbo].[PR_PARTNER_RECEIVABLE_DTL_LIST]
( 
	@P_VEN_CODE		NVARCHAR(7) = '',	-- 거래처코드
	@P_FROM_DT		NVARCHAR(8) = '',	-- FROM 일자
	@P_TO_DT		NVARCHAR(8) = ''	-- TO 일자
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	


		SELECT 
		ISNULL(PORD.SALE_DT, '') AS SALE_DT,				--출고일자 
		ISNULL(PORD.ORD_NO, '') AS ORD_NO,					--주문번호
		PMST.VEN_CODE,										--거래처코드
		PMST.VEN_NAME,										--거래처명
		'' AS SALE_GB,										--정상/반품 상태등.. 노출(반품 상태 없음)
		ISNULL(PORD.SALE_TOTAL_AMT, 0) AS SALE_TOTAL_AMT,	--매출액
		ISNULL(PORD.DEPOSIT_AMT, 0) AS DEPOSIT_AMT,			--입금액
		ISNULL(PORD.RECEIVABLE_AMT, 0) AS RECEIVABLE_AMT	--미수금 
		FROM CD_PARTNER_MST AS PMST 
			LEFT OUTER JOIN (
				SELECT 
					SALE_DT,
					ORD_NO,
					VEN_CODE,
					SUM(SALE_TOTAL_AMT) AS SALE_TOTAL_AMT,
					SUM(DEPOSIT_AMT) + SUM(CASE WHEN DEPOSIT_AMT < 0 THEN -DEPOSIT_AMT ELSE 0 END)  AS DEPOSIT_AMT,
					SUM(CASE WHEN DEPOSIT_AMT < 0 THEN DEPOSIT_AMT ELSE SALE_TOTAL_AMT - DEPOSIT_AMT END) AS RECEIVABLE_AMT
				FROM PA_ACCT_DEPOSIT_ORD 						
				WHERE SALE_DT BETWEEN @P_FROM_DT AND @P_TO_DT					
				GROUP BY SALE_DT, ORD_NO, VEN_CODE
			) AS PORD ON PMST.VEN_CODE = PORD.VEN_CODE
			WHERE PMST.VEN_CODE = @P_VEN_CODE


	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

