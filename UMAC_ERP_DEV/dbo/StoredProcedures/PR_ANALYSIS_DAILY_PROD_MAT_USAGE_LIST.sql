/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.09.02
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 생산/부자재사용량(제품생산) 조회
-- 실행문 : EXEC PR_ANALYSIS_DAILY_PROD_MAT_USAGE_LIST '','','','',''
*/
CREATE PROCEDURE [dbo].[PR_ANALYSIS_DAILY_PROD_MAT_USAGE_LIST]
( 
	@P_REPORT_DATE		NVARCHAR(8) = '',	-- 거래처코드
	@P_LOT_OIL_GB		NVARCHAR(2) = '',	-- 유종
	@P_LOT_PARTNER_GB	NVARCHAR(3) = '',	-- 거래처 구분
	@P_TAB_INDEX	 	NVARCHAR(1) = ''	-- 중분류 코드
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	

----------------------------------------------------
-- 제품생산 조회
----------------------------------------------------
		IF @P_TAB_INDEX = '1'
		BEGIN
			SELECT ROW_NUMBER() OVER(ORDER BY A.SCAN_CODE) AS ROW_NUM
				 , A.PROD_DT AS REPORT_DATE
				 , C.CD_NM AS LOT_PARTNER_GB_NM
				 , A.SCAN_CODE
				 , B.ITM_NAME
				 , A.PROD_APP_QTY AS PROD_QTY
				 , ISNULL((CAST(B.NET_WEIGHT AS DECIMAL(15,2)) / 1000) * A.PROD_QTY, 0) AS WEIGHT
				 , A.LOT_NO
				 , ISNULL(D.REMARKS, '') AS REMARKS
				FROM CD_LOT_MST AS A
				INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
				INNER JOIN TBL_COMM_CD_MST AS C ON B.LOT_PARTNER_GB = C.CD_ID AND C.CD_CL = 'LOT_PARTNER_GB'
				LEFT OUTER JOIN RP_ANALYSIS_DAILY_PROD_MAT_USAGE AS D ON A.PROD_DT = D.REPORT_DATE AND A.SCAN_CODE = D.SCAN_CODE
			   WHERE A.PROD_DT = @P_REPORT_DATE
				 AND 1=(CASE WHEN @P_LOT_OIL_GB = '' THEN 1 WHEN @P_LOT_OIL_GB != '' AND B.LOT_OIL_GB = @P_LOT_OIL_GB THEN 1 ELSE 2 END)
				 AND 1=(CASE WHEN @P_LOT_PARTNER_GB = '' THEN 1 WHEN @P_LOT_PARTNER_GB != '' AND B.LOT_PARTNER_GB = @P_LOT_PARTNER_GB THEN 1 ELSE 2 END)
				 AND B.ITM_FORM NOT IN ('3', '4')
				 AND A.CFM_FLAG = 'Y'
				 --AND A.PROD_GB = 'B'
			;
		END

		ELSE IF @P_TAB_INDEX = '2'
		BEGIN
			WITH W_MAT_USAGE AS (
				SELECT A.PROD_DT AS REPORT_DATE
					 , A.COMP_CODE AS SCAN_CODE
					 , CAST(SUM(A.INPUT_QTY) AS DECIMAL(15,2)) AS USAGE_QTY
					FROM PD_MAT_USAGE AS A
				   WHERE PROD_DT = @P_REPORT_DATE
					 AND A.MAT_GB != '1'
				   GROUP BY A.PROD_DT, A.COMP_CODE
			
			), W_BOM_HIST AS (
				SELECT A.PROD_DT AS REPORT_DATE
					 , A.BOM_SET_COMP_CD AS SCAN_CODE
					 , CAST(A.COMP_QTY * (-1) AS DECIMAL(15,2)) AS USAGE_QTY
					FROM (
						SELECT PROD_DT
							 , BOM_SET_COMP_CD
							 , SUM(COMP_APP_QTY) AS COMP_QTY
							FROM PD_BOM_SET_HIST AS A
						   WHERE PROD_DT = @P_REPORT_DATE
						     AND A.CFM_FLAG = 'Y'
						   GROUP BY PROD_DT, BOM_SET_COMP_CD
					) AS A
			), W_RESULT AS (
				SELECT * FROM W_MAT_USAGE
				UNION ALL
				SELECT * FROM W_BOM_HIST
			)
			SELECT ROW_NUMBER() OVER(ORDER BY A.SCAN_CODE) AS ROW_NUM 
			     , A.REPORT_DATE
				 , A.SCAN_CODE
				 , ISNULL(C.CD_NM, '') AS LOT_PARTNER_GB_NM
				 , B.ITM_NAME
				 , A.USAGE_QTY
				 , ISNULL(D.REMARKS, '') AS REMARKS
				 , B.WEIGHT_GB
				FROM W_RESULT AS A 
				INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
				LEFT OUTER JOIN TBL_COMM_CD_MST AS C ON B.LOT_PARTNER_GB = C.CD_ID AND C.CD_CL = 'LOT_PARTNER_GB'
				LEFT OUTER JOIN RP_ANALYSIS_DAILY_PROD_MAT_USAGE AS D ON A.REPORT_DATE = D.REPORT_DATE AND A.SCAN_CODE = D.SCAN_CODE
			   WHERE B.ITM_FORM != '3'
				 AND 1=(CASE WHEN @P_LOT_OIL_GB = '' THEN 1 WHEN @P_LOT_OIL_GB != '' AND B.LOT_OIL_GB = @P_LOT_OIL_GB THEN 1 ELSE 2 END)
				 AND 1=(CASE WHEN @P_LOT_PARTNER_GB = '' THEN 1 WHEN @P_LOT_PARTNER_GB != '' AND B.LOT_PARTNER_GB = @P_LOT_PARTNER_GB THEN 1 ELSE 2 END)
		END

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

