
/*
-- 생성자 :	최수민
-- 등록일 :	2024.07.18
-- 설 명  : 대시보드 출고 예정 & 출고 완료 목록 조회
-- 수정자 :	최수민
-- 수정일 :	2024.07.18
			2024.11.29 강세미 - 배차여부 수정
-- 설 명  : 
-- 실행문 : 
	EXEC PR_DASH_ORDER_LIST '20240724', '20240807', '25'
	EXEC PR_DASH_ORDER_LIST '20240807', '20240807', NULL
*/
CREATE PROCEDURE [dbo].[PR_DASH_ORDER_LIST]
(
	@P_START_DT		NVARCHAR(8) = '',			-- 조회시작일자
	@P_END_DT		NVARCHAR(8) = '',			-- 조회종료일자
	@P_ORD_STAT	    NVARCHAR(2) = NULL			-- 주문 상태
)
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;


	BEGIN TRY 

		WITH VIEW_ORDERED_PRD AS (
			SELECT HDR.ORD_NO
				 , HDR.DELIVERY_REQ_DT
				 , HDR.ORD_STAT
				 , HDR.VEN_CODE
				 , HDR.DELIVERY_CODE
				 , HDR.ORD_DT
				 , DTL.SCAN_CODE
				 , CMN.ITM_FORM
				 , CASE WHEN ITM_FORM IN ('1', '2') THEN '0'
						WHEN ITM_FORM = '3' THEN '3'
						WHEN ITM_FORM = '4' THEN '4'
						ELSE '5' END AS ITM_FORM_DESC
				 , ROW_NUMBER() OVER (PARTITION BY HDR.ORD_NO ORDER BY DTL.SCAN_CODE) AS RN
				 , CASE WHEN HDR.TRANS_YN = 'F' THEN 'FNR배차' WHEN HDR.TRANS_YN = 'Y' THEN '일반배차' ELSE '배차불필요' END AS TRANS_YN	--배차여부
			  FROM PO_ORDER_HDR AS HDR
			 INNER JOIN PO_ORDER_DTL AS DTL ON HDR.ORD_NO = DTL.ORD_NO
			 INNER JOIN CD_PRODUCT_CMN AS CMN ON DTL.SCAN_CODE = CMN.SCAN_CODE
			 WHERE HDR.DELIVERY_REQ_DT BETWEEN @P_START_DT AND @P_END_DT
			   AND ((@P_ORD_STAT IS NULL AND HDR.ORD_STAT IN ('10','25','33','35','40'))
				OR (@P_ORD_STAT IS NOT NULL AND HDR.ORD_STAT = @P_ORD_STAT))
		)
		SELECT ROW_NUMBER() OVER(ORDER BY S.ENTRANCE_TIME DESC, DELIVERY_REQ_DT, ITM_FORM_DESC) AS ROW_NUM
			 , A.ORD_NO
			 , A.ITM_FORM_DESC
			 , A.ORD_STAT
			 , A.ORD_DT
			 , A.DELIVERY_REQ_DT
			 , A.TRANS_YN
			 , C.VEN_NAME
			 , S.ENTRANCE_TIME
			 , F.CD_NM AS ORD_STAT_NM
		  FROM VIEW_ORDERED_PRD AS A
		 INNER JOIN CD_PARTNER_MST AS C ON C.VEN_CODE = A.VEN_CODE
		  LEFT OUTER JOIN CD_PARTNER_DELIVERY AS E ON A.VEN_CODE = E.VEN_CODE AND E.DELIVERY_CODE = A.DELIVERY_CODE
		  LEFT OUTER JOIN TBL_COMM_CD_MST AS F ON F.CD_CL = 'ORD_STAT' AND A.ORD_STAT = F.CD_ID
		  LEFT OUTER JOIN PO_SCALE AS S ON A.ORD_NO = S.ORD_NO
		 WHERE RN = 1
		 ORDER BY S.ENTRANCE_TIME DESC, DELIVERY_REQ_DT, ITM_FORM_DESC;

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

