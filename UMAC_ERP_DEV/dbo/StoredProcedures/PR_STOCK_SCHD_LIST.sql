/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.07.04
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 재고조사 일정 조회
-- 실행문 : EXEC PR_STOCK_SCHD_LIST '',''
*/
CREATE PROCEDURE [dbo].[PR_STOCK_SCHD_LIST]
( 
	@P_FROM_DT	 NVARCHAR(8) = '',		-- FROM_DT
	@P_TO_DT	 NVARCHAR(8) = '',		-- TO_DT
	@P_SURVEY_ID NVARCHAR(50) = NULL	-- SURVEY_ID
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
		
		WITH W_DATA AS (
			SELECT A.INV_DT
				 , A.SURVEY_ID
				 , A.SURVEY_GB
				 , A.IEMP_ID
				 , CASE WHEN MAX(B.SURVEY_ID) IS NULL THEN 'Y' ELSE 'N' END AS DEL_FG
				 , A.CFM_FLAG
				 , A.CFM_DT
				FROM IV_SCHEDULER AS A
				LEFT OUTER JOIN IV_STOCK_SURVEY AS B ON A.SURVEY_ID = B.SURVEY_ID
			   WHERE 
					(@P_SURVEY_ID IS NOT NULL AND A.SURVEY_ID = @P_SURVEY_ID) OR 
					(@P_SURVEY_ID IS NULL AND A.INV_DT BETWEEN @P_FROM_DT AND @P_TO_DT)
			   GROUP BY A.INV_DT, A.SURVEY_ID, A.SURVEY_GB, A.IEMP_ID, A.CFM_FLAG, A.CFM_DT
		)
		SELECT ROW_NUMBER() OVER(ORDER BY A.SURVEY_ID DESC) AS ROW_NUM
		     , A.SURVEY_ID
			 , A.INV_DT
			 , A.DEL_FG
			 , A.SURVEY_GB
			 , B.CD_NM AS SURVEY_GB_NM
			 , C.USER_NM AS IEMP_NM
		     , A.CFM_FLAG
			 , A.CFM_DT
			FROM W_DATA AS A
			INNER JOIN TBL_COMM_CD_MST AS B ON CD_CL = 'INV_SURVEY_GB' AND A.SURVEY_GB = B.CD_ID
			INNER JOIN TBL_USER_MST AS C ON A.IEMP_ID = C.USER_ID

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

