
/*
-- 생성자 :	최수민
-- 등록일 :	2024.09.04
-- 설 명  : 대시보드 제품별 출고량 (상위 10개 내림차순)
-- 수정자 :	-
-- 수정일 :	-
-- 설 명  : 
-- 실행문 : EXEC PR_DASH_AGGR_ORDER_ITM_QTY '20240801' ,'20240831', 'DW,DS', 'QTY'
			EXEC PR_DASH_AGGR_ORDER_ITM_QTY '20240801' ,'20240831', NULL, NULL

*/
CREATE PROCEDURE [dbo].[PR_DASH_AGGR_ORDER_ITM_QTY]
(
	@P_START_DT				NVARCHAR(8) = '',			-- 조회시작일자
	@P_END_DT				NVARCHAR(8) = '',			-- 조회종료일자
	@P_LOT_PARTNER_GB	    NVARCHAR(25) = '',			-- LOT 거래처 구분
	@P_WEIGHT_GB			NVARCHAR(3) = ''			-- 수중량구분(QTY, WT)
)
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;


	BEGIN TRY 

			
		WITH CURR_DATA AS (
			SELECT TOP 10
				   AO.SCAN_CODE
				 , CMN.ITM_NAME
				 , ISNULL(SUM(AO.AGGR_KG_QTY), 0) AS AGGR_SUM1
				 , ISNULL(SUM(AO.AGGR_EA_QTY), 0) AS AGGR_EA_SUM1
			  FROM AG_ORDER_ITM AS AO
			 INNER JOIN CD_PRODUCT_CMN AS CMN ON AO.SCAN_CODE = CMN.SCAN_CODE
			 WHERE AGGR_DT BETWEEN @P_START_DT AND @P_END_DT
			   AND (
						ISNULL(@P_LOT_PARTNER_GB, '') = '' 
						OR AO.LOT_PARTNER_GB IN (
							SELECT value FROM STRING_SPLIT(@P_LOT_PARTNER_GB, ',')
						)
					)
			   AND (
						ISNULL(@P_WEIGHT_GB, '') = '' 
						OR AO.WEIGHT_GB = @P_WEIGHT_GB
					)
			   AND NOT EXISTS (
					SELECT 1
					FROM CD_PRODUCT_CMN AS CPC
					WHERE CPC.SCAN_CODE = AO.SCAN_CODE
					AND CPC.LOT_OIL_GB IN ('CG', 'ETC')
				)
			 GROUP BY AO.SCAN_CODE, CMN.ITM_NAME
			 ORDER BY ISNULL(SUM(AO.AGGR_KG_QTY), 0) DESC
		), PREV_DATA AS (
			SELECT --TOP 20
				   AO.SCAN_CODE
				 , CMN.ITM_NAME
				 , ISNULL(SUM(AO.AGGR_KG_QTY), 0) AS AGGR_SUM2
				 , ISNULL(SUM(AO.AGGR_EA_QTY), 0) AS AGGR_EA_SUM2
			  FROM AG_ORDER_ITM AS AO
			 INNER JOIN CD_PRODUCT_CMN AS CMN ON AO.SCAN_CODE = CMN.SCAN_CODE
			 WHERE AO.AGGR_DT BETWEEN CAST(DATEADD(YEAR, -1, @P_START_DT) AS NVARCHAR) AND CAST(DATEADD(YEAR, -1, @P_END_DT) AS NVARCHAR)
			   AND (
						ISNULL(@P_LOT_PARTNER_GB, '') = '' 
						OR AO.LOT_PARTNER_GB IN (
							SELECT value FROM STRING_SPLIT(@P_LOT_PARTNER_GB, ',')
						)
					)
			   AND (
						ISNULL(@P_WEIGHT_GB, '') = '' 
						OR AO.WEIGHT_GB = @P_WEIGHT_GB
					)
			 GROUP BY AO.SCAN_CODE, CMN.ITM_NAME

		)
		SELECT CURR.SCAN_CODE
			 , CURR.ITM_NAME
			 , ISNULL(CURR.AGGR_SUM1, 0) AS CUR_AGGR_SUM
			 , ISNULL(PREV.AGGR_SUM2, 0) AS PRE_AGGR_SUM
			 , CONCAT(FORMAT(ISNULL(CURR.AGGR_EA_SUM1, 0), 'N0'), ' EA') AS CUR_AGGR_EA_SUM
			 , CONCAT(FORMAT(ISNULL(PREV.AGGR_EA_SUM2, 0), 'N0'), ' EA') AS PRE_AGGR_EA_SUM
		  FROM CURR_DATA AS CURR
		  LEFT JOIN PREV_DATA AS PREV ON CURR.SCAN_CODE = PREV.SCAN_CODE
		  ORDER BY CURR.AGGR_SUM1 DESC;


	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH

END

GO

