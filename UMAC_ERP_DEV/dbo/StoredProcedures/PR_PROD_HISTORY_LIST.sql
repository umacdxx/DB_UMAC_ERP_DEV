/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.05.02
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 생산이력 조회
-- 실행문 : EXEC PR_PROD_HISTORY_LIST '','','','',''
*/
CREATE PROCEDURE [dbo].[PR_PROD_HISTORY_LIST]
	@P_SCAN_CODE		VARCHAR(14),	-- 상품코드
	@P_FROM_PROD_DT		VARCHAR(10),	-- from 생산일자
	@P_TO_PROD_DT		VARCHAR(10),	-- to 생산일자
	@P_LOT_OIL_GB		VARCHAR(2),		-- 유종
	@P_LOT_PARTNER_GB	VARCHAR(3)		-- 거래처 구분
AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 
	
	-- 에러발생 테스트
		--select 1/0
		SELECT (ISNULL([D01], 0)+ISNULL([D02], 0)+ISNULL([D03], 0)+ISNULL([D04], 0)+ISNULL([D05], 0)+ISNULL([D06], 0)+ISNULL([D07], 0)+ISNULL([D08], 0)+ISNULL([D09], 0)+ISNULL([D10], 0)
		       +ISNULL([D11], 0)+ISNULL([D12], 0)+ISNULL([D13], 0)+ISNULL([D14], 0)+ISNULL([D15], 0)+ISNULL([D16], 0)+ISNULL([D17], 0)+ISNULL([D18], 0)+ISNULL([D19], 0)+ISNULL([D20], 0)
		       +ISNULL([D21], 0)+ISNULL([D22], 0)+ISNULL([D23], 0)+ISNULL([D24], 0)+ISNULL([D25], 0)+ISNULL([D26], 0)+ISNULL([D27], 0)+ISNULL([D28], 0)+ISNULL([D29], 0)+ISNULL([D30], 0)+ISNULL([D31], 0)) 
				AS VIEW_SUM_DATA
		     , *
			FROM (
				SELECT CONCAT('D',A.DAY) AS DAY
				     , A.SCAN_CODE
				     , A.VIEW_NAME
				     , A.VIEW_UNIT
				     , A.VIEW_DATA
					FROM (
						SELECT SUBSTRING(A.PROD_DT, 7,2) AS DAY
						     , A.SCAN_CODE
						     , A.PROD_APP_QTY AS PROD_QTY
						     , '' AS ITM_NAME
						     , 0 AS WEIGHT
						     , A.SCAN_CODE AS VIEW_NAME
						     , A.PROD_APP_QTY AS VIEW_DATA
						     , 'EA' AS VIEW_UNIT
							FROM CD_LOT_MST AS A
							INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
						   WHERE 1=1
						     AND A.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT
						     AND 1 = (CASE WHEN @P_SCAN_CODE 		= '' THEN 1 WHEN @P_SCAN_CODE 		!= '' AND A.SCAN_CODE 		= @P_SCAN_CODE 		THEN 1 ELSE 2 END)
						     AND 1 = (CASE WHEN @P_LOT_OIL_GB 		= '' THEN 1 WHEN @P_LOT_OIL_GB 		!= '' AND B.LOT_OIL_GB		= @P_LOT_OIL_GB 	THEN 1 ELSE 2 END)
						     AND 1 = (CASE WHEN @P_LOT_PARTNER_GB	= '' THEN 1 WHEN @P_LOT_PARTNER_GB	!= '' AND B.LOT_PARTNER_GB 	= @P_LOT_PARTNER_GB	THEN 1 ELSE 2 END)
							 AND B.ITM_FORM NOT IN ('3', '4')
							 AND A.CFM_FLAG = 'Y'
						UNION ALL
						SELECT SUBSTRING(A.PROD_DT, 7,2) AS DAY
						     , A.SCAN_CODE
						     , A.PROD_APP_QTY AS PROD_QTY
						     , B.ITM_NAME
						     , ROUND(A.PROD_APP_QTY * B.UNIT_CAPACITY / 1000 * 0.9167, 1) AS WEIGHT
						     , B.ITM_NAME AS VIEW_NAME
						     , ROUND(A.PROD_APP_QTY * B.UNIT_CAPACITY / 1000 * 0.9167, 1) AS VIEW_DATA
						     , 'KG' AS VIEW_UNIT
							FROM CD_LOT_MST AS A
							INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
						   WHERE 1=1
						     AND A.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT
						     AND 1 = (CASE WHEN @P_SCAN_CODE 		= '' THEN 1 WHEN @P_SCAN_CODE 		!= '' AND A.SCAN_CODE 		= @P_SCAN_CODE THEN 1 ELSE 2 END)
						     AND 1 = (CASE WHEN @P_LOT_OIL_GB 		= '' THEN 1 WHEN @P_LOT_OIL_GB 		!= '' AND B.LOT_OIL_GB		= @P_LOT_OIL_GB 	THEN 1 ELSE 2 END)
						     AND 1 = (CASE WHEN @P_LOT_PARTNER_GB	= '' THEN 1 WHEN @P_LOT_PARTNER_GB	!= '' AND B.LOT_PARTNER_GB 	= @P_LOT_PARTNER_GB	THEN 1 ELSE 2 END)
							 AND B.ITM_FORM NOT IN ('3', '4')
							 AND A.CFM_FLAG = 'Y'
					) AS A
			) AS MAIN
		   PIVOT (
		   		SUM(MAIN.VIEW_DATA) FOR DAY IN ([D01],[D02],[D03],[D04],[D05],[D06],[D07],[D08],[D09],[D10],[D11],[D12],[D13],[D14],[D15],[D16],[D17],[D18],[D19],[D20],[D21],[D22],[D23],[D24],[D25],[D26],[D27],[D28],[D29],[D30],[D31])
		   ) AS pivot_result
		   ORDER BY SCAN_CODE, VIEW_UNIT

		
	END TRY
	BEGIN CATCH	
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH

END

GO

