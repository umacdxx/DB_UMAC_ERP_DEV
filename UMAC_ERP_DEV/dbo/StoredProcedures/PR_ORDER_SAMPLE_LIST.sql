/*
-- 생성자 :	강세미
-- 등록일 :	2024.07.18
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 주문등록 샘플 조회
-- 실행문 : 
EXEC PR_ORDER_SAMPLE_LIST '2240624001'
*/
CREATE PROCEDURE [dbo].[PR_ORDER_SAMPLE_LIST]
( 
	@P_ORD_NO	NVARCHAR(11) = ''   -- 주문번호
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY 
		SELECT ROW_NUMBER() OVER(ORDER BY SEQ) AS ROW_NUM,
				 A.ORD_NO,
				 A.SEQ,
				 A.SCAN_CODE,
				 A.ITM_GB,
				 A.IPSU_QTY,
				 A.ORD_QTY,		
				 A.TAX_GB,
				 CAST(ISNULL(A.PICKING_QTY,0) AS DECIMAL(15,2)) AS PICKING_QTY,
				 A.PICKING_EMP_NO,
				 A.PICKING_SDATE,
				 A.PICKING_EDATE,
				 B.WEIGHT_GB,
				 B.ITM_NAME,
				 B.ITM_NAME_DETAIL,
				 B.UNIT,
				 ISNULL(B.UNIT_CAPACITY, '') AS UNIT_CAPACITY,	
				 C.CD_NM AS TAX_GB_NM,
				 A.REMARKS,
				 D.CD_NM AS ITM_GB_NM,
				 E.CD_NM AS UNIT_NM,
				 CASE WHEN B.ITM_FORM = '2' THEN F.BOX_EACH_QTY ELSE F.CUR_INV_QTY END AS CUR_INV_QTY,
				 CASE WHEN B.ITM_FORM = '2' THEN ISNULL(F.BOX_EACH_QTY,0) - ISNULL(G.TOTAL_ORD_QTY, 0) ELSE ISNULL(F.CUR_INV_QTY,0) - ISNULL(G.TOTAL_ORD_QTY, 0) END AS AVL_INV_QTY
		  FROM PO_ORDER_SAMPLE AS A
		  INNER JOIN CD_PRODUCT_CMN AS B
					 ON B.SCAN_CODE = A.SCAN_CODE
		  INNER JOIN TBL_COMM_CD_MST AS C
					 ON C.CD_CL = 'TAX_GB' AND A.TAX_GB = C.CD_ID
		  INNER JOIN TBL_COMM_CD_MST AS D
					 ON D.CD_CL = 'ITM_GB' AND A.ITM_GB = D.CD_ID
		  INNER JOIN TBL_COMM_CD_MST AS E
					 ON E.CD_CL = 'UNIT' AND B.UNIT = E.CD_ID
		  LEFT OUTER JOIN (
									SELECT A.SCAN_CODE
									 	 , A.ITM_CODE
									 	 , D.CUR_INV_QTY
									 	 , C.CUR_INV_QTY AS BOX_EACH_QTY
									 FROM CD_PRODUCT_CMN AS A
									 LEFT OUTER JOIN IV_PRODUCT_STAT AS D ON A.SCAN_CODE = D.SCAN_CODE
									 LEFT OUTER JOIN CD_BOX_MST AS B ON A.ITM_CODE = B.BOX_CODE
									 LEFT OUTER JOIN IV_PRODUCT_STAT AS C ON B.ITM_CODE = C.ITM_CODE
								) AS F
		 ON A.SCAN_CODE = F.SCAN_CODE
		 LEFT OUTER JOIN VIEW_TOTAL_ORDER_QTY AS G ON B.ITM_CODE = G.ITM_CODE
		 WHERE A.ORD_NO = @P_ORD_NO

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

