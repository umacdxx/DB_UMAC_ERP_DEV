/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.05.08
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 시험성적서 기초정보
-- 실행문 : EXEC PR_TEST_REPORT_LIST  '','','','',''
*/
CREATE PROCEDURE [dbo].[PR_TEST_REPORT_LIST]
	@P_SCAN_CODE		VARCHAR(14),	-- 상품코드
	@P_FROM_UDATE		VARCHAR(10),	-- from 수정일자
	@P_TO_UDATE			VARCHAR(10),	-- to 수정일자
	@P_LOT_OIL_GB		VARCHAR(2),		-- 유종
	@P_LOT_PARTNER_GB	VARCHAR(3),		-- 거래처 구분
	@P_ALL_SEARCH		VARCHAR(2),		-- 전체조회 여부
	@P_TOT_PAGE_ROW	 INT,	-- 한 페이지에 나타나는 row 수
	@P_PAGE_INDEX	 INT 	  -- page row 인덱스
AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 
		SELECT ROW_NUMBER() OVER(ORDER BY A.SCAN_CODE) AS ROW_NUM
		     , A.SCAN_CODE		
		     , CONVERT(NVARCHAR(8), A.UDATE, 112) AS UDATE
		     , A.RAW_MATERIALS    
		     , A.ITM_PROD_NO
		     , A.CHAR
		     , A.ACID
		     , A.PEROXIDE
		     , A.LODINE
		     , A.COLOR
		     , A.BENXO
		     , A.R_NO
			 , A.COLOR_TYPE
		     , B.ITM_NAME
		     , C.CD_NM AS LOT_OIL_GB
		     , D.CD_NM AS LOT_PARTNER_GB
    		 , COUNT(*) OVER(PARTITION BY 1) AS TOT_CNT
			FROM PD_TEST_REPORT_INFO AS A
			INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
			LEFT OUTER JOIN TBL_COMM_CD_MST AS C ON B.LOT_OIL_GB = C.CD_ID AND C.CD_CL = 'LOT_OIL_GB'
			LEFT OUTER JOIN TBL_COMM_CD_MST AS D ON B.LOT_PARTNER_GB = D.CD_ID AND D.CD_CL = 'LOT_PARTNER_GB'
		   WHERE 1 = (CASE WHEN @P_ALL_SEARCH = 'Y' THEN 1 WHEN @P_ALL_SEARCH = 'N' AND CONVERT(NVARCHAR(8), A.UDATE, 112) BETWEEN @P_FROM_UDATE AND @P_TO_UDATE THEN 1 ELSE 2 END)
		     --AND CONVERT(NVARCHAR(8), A.UDATE, 112) BETWEEN @P_FROM_UDATE AND @P_TO_UDATE
		     AND 1 = (CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE != '' AND A.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 2 END)
		     AND 1 = (CASE WHEN @P_LOT_OIL_GB = '' THEN 1 WHEN @P_LOT_OIL_GB != '' AND B.LOT_OIL_GB	= @P_LOT_OIL_GB THEN 1 ELSE 2 END)
		     AND 1 = (CASE WHEN @P_LOT_PARTNER_GB = '' THEN 1 WHEN @P_LOT_PARTNER_GB != '' AND B.LOT_PARTNER_GB = @P_LOT_PARTNER_GB	THEN 1 ELSE 2 END)
		ORDER BY A.SCAN_CODE
		OFFSET @P_PAGE_INDEX ROW
		FETCH NEXT @P_TOT_PAGE_ROW ROWS ONLY
			;
		
	END TRY
	BEGIN CATCH	
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH

END

GO

