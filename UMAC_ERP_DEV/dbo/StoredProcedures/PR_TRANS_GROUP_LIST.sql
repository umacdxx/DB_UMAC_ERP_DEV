/*
-- 생성자 :	강세미
-- 등록일 :	2023.04.22
-- 설 명  : 배차그룹 리스트 조회
-- 수정자 :	
-- 수정일 :	
-- 설 명  : 
-- 실행문 : 
EXEC PR_TRANS_GROUP_LIST '2240417002', '5'
*/
CREATE PROCEDURE [dbo].[PR_TRANS_GROUP_LIST]
( 
	@P_ORD_NO					NVARCHAR(11),		-- 주문번호
	@P_GROUP_NO					INT = 0				-- 그룹번호
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY
	
	SELECT A.ORD_NO
		, A.R_ADDR
		, A.R_ADDR_DTL		
		, A.ORD_STAT AS IO_STAT	
		, C.TRANS_SECTION	
		, D.DELIVERY_NAME
		, E.TRANS_COST
		, E.RENT_COST
		, E.GROUP_NO
		, E.GROSS_WGHT
		, E.UNLOAD_WGHT
		, F.LAND_GB
		, G.VEN_NAME
		, H.ITM_NAME + (CASE WHEN B.ITM_CNT > 1 THEN ' 외 ' + CAST(B.ITM_CNT - 1 AS VARCHAR) + '건' ELSE '' END) AS ITM_NAME	
 	FROM PO_ORDER_HDR AS A INNER JOIN (
				SELECT A.ORD_NO, COUNT(A.ORD_NO) AS ITM_CNT, MAX(B.SCAN_CODE) AS SCAN_CODE
					FROM PO_ORDER_HDR AS A	
						INNER JOIN PO_ORDER_DTL AS B ON A.ORD_NO = B.ORD_NO
						LEFT OUTER JOIN PO_SCALE AS C ON C.ORD_NO = A.ORD_NO
					WHERE C.GROUP_NO = @P_GROUP_NO AND C.ORD_NO != @P_ORD_NO
				GROUP BY A.ORD_NO
	) B ON A.ORD_NO = B.ORD_NO
	LEFT OUTER JOIN CD_DELIVERY_PRICE AS C ON A.DELIVERY_PRICE_SEQ = C.SEQ
	LEFT OUTER JOIN CD_PARTNER_DELIVERY AS D ON A.VEN_CODE = D.VEN_CODE AND A.DELIVERY_CODE = D.DELIVERY_CODE 
	LEFT OUTER JOIN PO_SCALE AS E ON E.ORD_NO = A.ORD_NO
	LEFT OUTER JOIN PO_SCALE_GROUP AS F ON F.ORD_NO = A.ORD_NO
	INNER JOIN CD_PARTNER_MST AS G ON G.VEN_CODE = A.VEN_CODE
	INNER JOIN CD_PRODUCT_CMN AS H ON H.SCAN_CODE = B.SCAN_CODE
	 
	END TRY

	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END


--EXEC PR_TRANS_MANAGE_LIST '','','','20240101','20240402','','','','','','','100','0' 

GO

