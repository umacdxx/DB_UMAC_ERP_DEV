/*
-- 생성자 :	강세미
-- 등록일 :	2024.01.10
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 비밀번호 변경
-- 실행문 : EXEC PR_LOGIN_PASSWD_CHANGE 'admin', 'admin1234', '1234'
*/
CREATE PROCEDURE [dbo].[PR_LOGIN_PASSWD_CHANGE]
	@P_USER_ID NVARCHAR(20),			-- 아이디
	@P_PASSWD_NO NVARCHAR(256), 	   -- 기존 패스워드
	@P_NEW_PASSWD_NO NVARCHAR(256) 	   -- 변경할 패스워드
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	DECLARE @PASSWD_NO_B BINARY(20)				-- 기존 패스워드 암호화
	DECLARE @NEW_PASSWD_NO_B BINARY(20)				-- 변경할 패스워드 암호화
	DECLARE @PASSWD_CHECK VARCHAR(1)				-- 기존 패스워드 일치여부

	DECLARE @RETURN_CODE INT = 0					-- 리턴코드
	DECLARE @RETURN_MESSAGE NVARCHAR(100) = ''		-- 리턴메시지

	BEGIN TRY 
	-- 패스워드 암호화
	SET @PASSWD_NO_B = CONVERT(BINARY(20), HASHBYTES('SHA1',@P_PASSWD_NO))
	SET @NEW_PASSWD_NO_B = CONVERT(BINARY(20), HASHBYTES('SHA1',@P_NEW_PASSWD_NO))
	
	SELECT @PASSWD_CHECK = (CASE WHEN @PASSWD_NO_B = PASSWD_NO THEN 'Y' ELSE 'N' END)
	  FROM TBL_USER_MST
	 WHERE USER_ID = @P_USER_ID
	
	IF @PASSWD_CHECK = 'Y'
		BEGIN
			UPDATE TBL_USER_MST 
				SET PASSWD_NO = @NEW_PASSWD_NO_B
				  , PWD_COUNT = 0
			 WHERE USER_ID = @P_USER_ID
			 
			SET @RETURN_CODE = 0
			SET @RETURN_MESSAGE = '저장되었습니다.'
		END
	ELSE
		BEGIN
			SET @RETURN_CODE = 9
			SET @RETURN_MESSAGE = '비밀번호가 일치하지 않습니다.'
		END

	END TRY
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	

		SET @RETURN_CODE = -1
		SET @RETURN_MESSAGE = '변경 실패하였습니다.'
	END CATCH
	
	SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE
	 
END

GO

