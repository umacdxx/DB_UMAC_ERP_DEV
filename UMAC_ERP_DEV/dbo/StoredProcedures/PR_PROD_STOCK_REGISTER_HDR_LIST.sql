/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.04.11
-- 수 정  : 2024.11.01 최수민 EXPIRATION_DT 제품 마스터에서 계산해서 보여주는 부분 주석
-- 설 명  : 생산재고 조회
-- 실행문 : EXEC PR_PROD_STOCK_REGISTER_HDR_LIST '','20240401','20240411','','','','','B'; 
*/
CREATE PROCEDURE [dbo].[PR_PROD_STOCK_REGISTER_HDR_LIST]
	@P_SCAN_CODE		VARCHAR(14),	-- 상품코드
	@P_FROM_PROD_DT		VARCHAR(10),	-- from 생산일자
	@P_TO_PROD_DT		VARCHAR(10),	-- to 생산일자
	@P_LRG_CODE			VARCHAR(2),		-- 대분류
	@P_MID_CODE			VARCHAR(4),		-- 중분류
	@P_LOT_OIL_GB		VARCHAR(2),		-- 유종
	@P_LOT_PARTNER_GB	VARCHAR(3),		-- 거래처 구분
	@P_PROD_GB			VARCHAR(1)		-- 생산구분
AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 
	
		--SELECT ROW_NUMBER() OVER(ORDER BY A.PROD_DT DESC, ISNULL(A.UDATE, A.IDATE) DESC) AS ROW_NUM
		SELECT ROW_NUMBER() OVER(ORDER BY A.PROD_DT DESC, A.CFM_FLAG, B.SCAN_CODE) AS ROW_NUM
		     , A.PROD_DT
			 , B.SCAN_CODE
			 , B.ITM_NAME
			 , D.LRG_NAME
			 , C.MID_CODE
			 , C.MID_NAME
			 , B.LOT_OIL_GB
			 , B.LOT_PARTNER_GB
			 , A.LOT_NO
			 , CASE WHEN MST_BOX.IPSU_QTY IS NULL THEN 0 ELSE FLOOR(A.PROD_QTY / MST_BOX.IPSU_QTY) END AS BOX_QTY
			 , A.PROD_QTY
			 , CASE WHEN MST_BOX.IPSU_QTY IS NULL THEN 0 ELSE FLOOR(A.PROD_APP_QTY / MST_BOX.IPSU_QTY) END AS BOX_APP_QTY
			 , A.PROD_APP_QTY
			 , A.CFM_FLAG AS CONFIRM_YN
			 --, (B.NET_WEIGHT / 1000) * A.PROD_QTY AS WEIGHT
			 , (CAST(B.NET_WEIGHT AS DECIMAL(15,2)) / 1000) * A.PROD_APP_QTY AS WEIGHT
			 , ISNULL(B.PLT_BOX_QTY, 0) AS PLT_BOX_QTY
			 , ISNULL(B.IPSU_QTY, 0 ) AS IPSU_QTY
			 , MST_USER.USER_NM AS IEMP_ID
			 , MST_CFM_USER.USER_NM AS CFM_IEMP_ID
			 , A.CFM_DT
			 , CONVERT(NVARCHAR(8), A.IDATE, 112) AS IDATE
			 --, CONVERT(NVARCHAR(8), DATEADD(DAY, B.EXPIRY_CNT, A.PROD_DT), 112) AS EXPIRY_DATE
			 --, CONVERT(NVARCHAR(8), DATEADD(MONTH, B.EXPIRY_CNT, CONVERT(VARCHAR(8), DATEADD(DAY, -1, CONVERT(DATE, A.PROD_DT, 112)), 112) ), 112) AS EXPIRY_DATE
			 , A.EXPIRATION_DT AS EXPIRY_DATE
			 , A.EXPIRATION_DT
			 , A.PROD_GB_CD
			 , (CASE WHEN E.BOM_PROD_CD IS NOT NULL THEN CM.ITM_NAME_DETAIL ELSE '' END) AS PROD_GB_NM
			 , CASE WHEN F.LOT_NO IS NULL THEN 'N' ELSE 'Y' END AS TEST_YN
			FROM CD_LOT_MST AS A
			INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
			INNER JOIN CD_MID_MST AS C ON B.MID_CODE = C.MID_CODE
			INNER JOIN CD_LRG_MST AS D ON C.LRG_CODE = D.LRG_CODE
			LEFT OUTER JOIN CD_BOM_HDR	AS E ON A.PROD_GB_CD = E.BOM_CD 
			LEFT OUTER JOIN CD_PRODUCT_CMN AS CM ON E.BOM_PROD_CD = CM.SCAN_CODE
			LEFT OUTER JOIN PD_TEST_REPORT AS F ON A.PROD_DT = F.PROD_DT AND A.SCAN_CODE = F.SCAN_CODE
			LEFT OUTER JOIN TBL_USER_MST AS MST_USER ON A.IEMP_ID = MST_USER.USER_ID
			LEFT OUTER JOIN TBL_USER_MST AS MST_CFM_USER ON A.CFM_EMP_ID = MST_CFM_USER.USER_ID
			LEFT OUTER JOIN CD_BOX_MST AS MST_BOX ON B.ITM_CODE = MST_BOX.ITM_CODE
		   WHERE 1=1
		     AND A.PROD_GB = @P_PROD_GB
		     AND A.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT
		     AND 1 = (CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE != '' AND B.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 2 END)
		     AND 1 = (CASE WHEN @P_LRG_CODE = '' THEN 1 WHEN @P_LRG_CODE != '' AND C.LRG_CODE = @P_LRG_CODE THEN 1 ELSE 2 END)
		     AND 1 = (CASE WHEN @P_MID_CODE = '' THEN 1 WHEN @P_MID_CODE != '' AND C.MID_CODE = @P_MID_CODE THEN 1 ELSE 2 END)
		     AND 1 = (CASE WHEN @P_LOT_OIL_GB = '' THEN 1 WHEN @P_LOT_OIL_GB != '' AND B.LOT_OIL_GB = @P_LOT_OIL_GB THEN 1 ELSE 2 END)
		     AND 1 = (CASE WHEN @P_LOT_PARTNER_GB = '' THEN 1 WHEN @P_LOT_PARTNER_GB != '' AND B.LOT_PARTNER_GB = @P_LOT_PARTNER_GB	THEN 1 ELSE 2 END)

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

