/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.06.05
-- 수정자 : 2024.11.12 최수민 반품 LOT 로직 추가
			2024.12.04 최수민 조정사유가 SET일 경우, SET 테이블에도 확정/취소 처리
			2024.12.09 최수민 SET 마스터 테이블 -> 생산 SET 결과 디테일 테이블로 변경
-- 설 명  : 단품재고조정 확정 or 취소
-- 실행문 : EXEC PR_SINGLE_STOCK_AD_CONFIRM '','','','',''
*/
CREATE PROCEDURE [dbo].[PR_SINGLE_STOCK_AD_CONFIRM]
( 
	@P_SEQ		INT ,				-- SEQ
	@P_EMP_ID	NVARCHAR(20) = '',	-- 세션 아이디
	@P_FLAG		NVARCHAR(1) = ''	-- Y:확정, D:취소
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	DECLARE @P_SET_MENU_GB	NVARCHAR(2)		= '';	-- SET 메뉴 구분
	DECLARE @P_SET_PLAN_ID  NVARCHAR(11)	= '';	-- PD_SET_RESULT_COMP 작업번호
	DECLARE @P_SET_SEQ		INT				= 0;	-- PD_SET_RESULT_COMP 순번
	
	DECLARE @RETURN_CODE 	INT 			= 0		-- 리턴코드
	DECLARE @RETURN_MESSAGE NVARCHAR(10) 	= '' 	-- 리턴메시지
	
	BEGIN TRAN
	BEGIN TRY 	

		-- 확정 플래그가 N인 데이터만 저장
		IF (SELECT CFM_FLAG FROM IV_STOCK_ADJUST WHERE SEQ = @P_SEQ) = 'N'
		BEGIN

			-- SET 재고조정 코드
			SELECT @P_SET_MENU_GB = MST.MGMT_ENTRY_2
			  FROM IV_STOCK_ADJUST AS ADJ
			 INNER JOIN TBL_COMM_CD_MST AS MST ON ADJ.INV_GB = MST.CD_ID AND MST.CD_CL = 'INV_ADJ_GB' AND MST.MGMT_ENTRY_1 = 'SET'
			 WHERE SEQ = @P_SEQ

			-- 1. 확정 시
			IF @P_FLAG = 'Y'
			BEGIN
				UPDATE IV_STOCK_ADJUST
					SET CFM_FLAG = 'Y'
					  , CFM_DT = CONVERT(NVARCHAR(8), GETDATE(), 112)
					  , APP_QTY = REQ_QTY
					  , UDATE = GETDATE()
					  , UEMP_ID = @P_EMP_ID
				   WHERE SEQ = @P_SEQ
					 AND CFM_FLAG = 'N'
				   ;

				DECLARE @T_SCAN_CODE NVARCHAR(14), @T_CHG_QTY NUMERIC(15, 2), @T_LOT_NO NVARCHAR(30), @T_CH_LOT_NO NVARCHAR(30), @T_INV_GB NVARCHAR(2), @T_CH_SEQ INT;

				SELECT @T_SCAN_CODE = CASE WHEN B.ITM_FORM = '2' THEN ISNULL(D.SCAN_CODE, A.SCAN_CODE) ELSE A.SCAN_CODE END
					 , @T_CHG_QTY = CASE WHEN B.ITM_FORM = '2' THEN A.APP_QTY * ISNULL(C.IPSU_QTY, 1) ELSE A.APP_QTY END
					 , @T_LOT_NO = A.LOT_NO
					 , @T_CH_LOT_NO = CHST.LOT_NO
					 , @T_INV_GB = A.INV_GB
					 , @T_CH_SEQ = A.CH_SEQ
					FROM IV_STOCK_ADJUST AS A
					INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
					LEFT OUTER JOIN CD_BOX_MST AS C ON B.ITM_CODE = C.BOX_CODE
					LEFT OUTER JOIN CD_PRODUCT_CMN AS D ON C.ITM_CODE = D.ITM_CODE
					LEFT OUTER JOIN IV_STOCK_ADJUST AS CHST ON A.CH_SEQ = CHST.SEQ
				   WHERE A.SEQ = @P_SEQ
					 AND A.CFM_FLAG = 'Y'
				;
			
				--(공통) 상품 재고 업데이트 
				EXEC PR_IV_PRODUCT_STAT_HDR_PUT @T_SCAN_CODE, @T_CHG_QTY, 'PR_SINGLE_STOCK_AD_CONFIRM', 0, @T_LOT_NO, '', '', @RETURN_CODE OUTPUT, @RETURN_MESSAGE OUTPUT;


				--변경할 LOT가 있는 경우 재고조정 필요, CH_SEQ에 해당하는 SEQ는 확정으로 변경
				IF @T_CH_LOT_NO <> ''
				BEGIN
					--확정여부 변경
					UPDATE IV_STOCK_ADJUST
						SET CFM_FLAG = 'Y'
						  , CFM_DT = CONVERT(NVARCHAR(8), GETDATE(), 112)
						  , APP_QTY = REQ_QTY
						  , UDATE = GETDATE()
						  , UEMP_ID = @P_EMP_ID
					   WHERE SEQ = @T_CH_SEQ
						 AND CFM_FLAG = 'N'
					   ;

					SET @T_CHG_QTY = @T_CHG_QTY * -1;

					--(공통) 상품 재고 업데이트 
					EXEC PR_IV_PRODUCT_STAT_HDR_PUT @T_SCAN_CODE, @T_CHG_QTY, 'PR_SINGLE_STOCK_AD_CONFIRM', 0, @T_CH_LOT_NO, '', '', @RETURN_CODE OUTPUT, @RETURN_MESSAGE OUTPUT;
				END


				-- 조정사유가 SET 인 경우 PD_SET_PLAN_MST 확정구분 변경
				IF @P_SET_MENU_GB <> ''
				BEGIN

					SELECT @P_SET_PLAN_ID = COMP.SET_PLAN_ID
						 , @P_SET_SEQ = COMP.SEQ
					  FROM IV_STOCK_ADJUST AS ADJ
					 INNER JOIN PD_SET_RESULT_COMP AS COMP ON ADJ.SCAN_CODE = COMP.SET_COMP_CD AND ADJ.INV_DT = COMP.PROD_DT AND ADJ.LOT_NO = COMP.LOT_NO
					 WHERE ADJ.SEQ = @P_SEQ
					   AND COMP.RESTORE_YN = CASE WHEN @P_SET_MENU_GB = 'C' THEN 'Y' ELSE 'N' END

					UPDATE PD_SET_RESULT_COMP
					   SET COMP_CFM_FLAG = 'Y'
					 WHERE SET_PLAN_ID = @P_SET_PLAN_ID
					   AND SEQ = @P_SET_SEQ
				END

				SET @RETURN_CODE = 91; -- 저장완료
				SET @RETURN_MESSAGE = DBO.GET_ERR_MSG('91');
			END
			-- 취소 시
			ELSE IF @P_FLAG = 'D'
			BEGIN
				UPDATE IV_STOCK_ADJUST
					SET CFM_FLAG = 'D'
					  , UDATE = GETDATE()
					  , UEMP_ID = @P_EMP_ID
				   WHERE SEQ = @P_SEQ
				   ;

				--변경할 LOT가 있는 경우 동일하게 취소 처리
				IF EXISTS (SELECT 1 FROM IV_STOCK_ADJUST WHERE SEQ = @P_SEQ AND CH_SEQ IS NOT NULL)
				BEGIN
					UPDATE IV_STOCK_ADJUST
					SET CFM_FLAG = 'D'
						, UDATE = GETDATE()
						, UEMP_ID = @P_EMP_ID
					WHERE SEQ = (SELECT CH_SEQ FROM IV_STOCK_ADJUST WHERE SEQ = @P_SEQ);
				END

				
				-- 조정사유가 SET 인 경우 PD_SET_RESULT_COMP에서 데이터 삭제
				IF @P_SET_MENU_GB <> ''
				BEGIN

					DELETE FROM PD_SET_RESULT_COMP
					 WHERE EXISTS ( SELECT 1
									 FROM IV_STOCK_ADJUST AS ADJ
									WHERE ADJ.SEQ = @P_SEQ
									  AND PD_SET_RESULT_COMP.SET_COMP_CD = ADJ.SCAN_CODE
									  AND PD_SET_RESULT_COMP.PROD_DT = ADJ.INV_DT
									  AND PD_SET_RESULT_COMP.LOT_NO = ADJ.LOT_NO
								)
					   AND RESTORE_YN = CASE WHEN @P_SET_MENU_GB = 'C' THEN 'Y' ELSE 'N' END;
				END

			
				SET @RETURN_CODE = 92; -- 취소완료
				SET @RETURN_MESSAGE = DBO.GET_ERR_MSG('92');

			END
		
		END
		ELSE 
		BEGIN
			SET @RETURN_CODE = 91; -- 저장완료
			SET @RETURN_MESSAGE = DBO.GET_ERR_MSG('91');
		END

		COMMIT;
	END TRY
	
	BEGIN CATCH		
		IF @@TRANCOUNT > 0
		BEGIN
			ROLLBACK TRAN

			--에러 로그 테이블 저장
			INSERT INTO TBL_ERROR_LOG 
			SELECT ERROR_PROCEDURE()	-- 프로시저명
			, ERROR_MESSAGE()			-- 에러메시지
			, ERROR_LINE()				-- 에러라인
			, GETDATE();
		
			SET @RETURN_CODE = -1; -- 저장 실패
			SET @RETURN_MESSAGE = DBO.GET_ERR_MSG('-1');
			
			SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE
		END
			
	END CATCH

	SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE

END

GO

