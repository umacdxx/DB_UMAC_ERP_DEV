/*
-- 생성자 :	강세미
-- 등록일 :	2023.03.05
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 최근 발주이력 조회
-- 실행문 : 
EXEC PR_PURCHASE_HIS_LIST 'UM10002', 100, 0
*/
CREATE PROCEDURE [dbo].[PR_PURCHASE_HIS_LIST]
( 
	@P_VEN_CODE	NVARCHAR(7) = '',   -- 거래처코드
	@P_TOT_PAGE_ROW INT,			-- 한 페이지에 나타나는 row 수
	@P_PAGE_INDEX INT 				-- page row 인덱스
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY 

		DECLARE @TOT_CNT INT = 0;

		--# TOTAL COUNT #	 
		SELECT @TOT_CNT = COUNT(A.ORD_NO)
		FROM PO_PURCHASE_HDR AS A INNER JOIN (
					SELECT A.ORD_NO
						FROM PO_PURCHASE_HDR AS A	
							INNER JOIN PO_PURCHASE_DTL AS B ON A.ORD_NO = B.ORD_NO
							WHERE A.VEN_CODE = @P_VEN_CODE
							AND A.PUR_STAT >= '35'
					GROUP BY A.ORD_NO
		) B ON A.ORD_NO = B.ORD_NO	 
		
		SELECT ROW_NUMBER() OVER(ORDER BY T1.ORD_NO DESC, T1.SEQ) + @P_PAGE_INDEX AS ROW_NUM,
			   @TOT_CNT,
			   T1.*
		FROM(   
			 SELECT	A.ORD_NO,
					A.SEQ,
					C.DELIVERY_IN_DT,
					A.SCAN_CODE,
					A.ORD_QTY,
					A.PUR_QTY,
					ISNULL(A.PUR_WPRC, 0) AS PUR_WPRC,
					ISNULL(A.PUR_WVAT, 0) AS PUR_WVAT,
					ISNULL(A.PUR_WAMT, 0) AS PUR_WAMT,
					ISNULL(A.PUR_TOTAL_WPRC, 0) AS PUR_TOTAL_WPRC,
					B.ITM_NAME,
					B.WEIGHT_GB,
					B.ITM_NAME_DETAIL,
					B.UNIT_CAPACITY,
					ISNULL(B.BASE_SPRC,0) AS BASE_SPRC,
					ISNULL(B.BASE_SVAT,0) AS BASE_SVAT,
					C.VEN_CODE,
					D.CD_NM AS TAX_GB_NM,
					COUNT(*) OVER(PARTITION BY 1) AS TOT_CNT,
					CASE WHEN B.ITM_FORM = '2' THEN ISNULL(E.BOX_EACH_QTY,0) ELSE ISNULL(E.CUR_INV_QTY,0) END AS CUR_INV_QTY,
					CASE WHEN B.ITM_FORM = '2' THEN ISNULL(E.BOX_EACH_QTY,0) - ISNULL(F.TOTAL_ORD_QTY, 0) ELSE ISNULL(E.CUR_INV_QTY,0) - ISNULL(F.TOTAL_ORD_QTY, 0) END AS AVL_INV_QTY
			   FROM PO_PURCHASE_DTL AS A
				INNER JOIN CD_PRODUCT_CMN AS B
					 ON A.SCAN_CODE = B.SCAN_CODE
				INNER JOIN PO_PURCHASE_HDR AS C
					 ON A.ORD_NO = C.ORD_NO
				LEFT OUTER JOIN TBL_COMM_CD_MST AS D
					ON D.CD_CL = 'TAX_GB' AND D.CD_ID = B.TAX_GB
				LEFT OUTER JOIN (
								SELECT A.SCAN_CODE
										, A.ITM_CODE
										, D.CUR_INV_QTY
										, C.CUR_INV_QTY AS BOX_EACH_QTY
									FROM CD_PRODUCT_CMN AS A
									LEFT OUTER JOIN IV_PRODUCT_STAT AS D ON A.SCAN_CODE = D.SCAN_CODE
									LEFT OUTER JOIN CD_BOX_MST AS B ON A.ITM_CODE = B.BOX_CODE
									LEFT OUTER JOIN IV_PRODUCT_STAT AS C ON B.ITM_CODE = C.ITM_CODE
							) AS E
			 ON A.SCAN_CODE = E.SCAN_CODE
			 LEFT OUTER JOIN VIEW_TOTAL_ORDER_QTY AS F ON B.ITM_CODE = F.ITM_CODE
			  WHERE C.VEN_CODE = @P_VEN_CODE
				 AND C.PUR_STAT >= '35'
			  ORDER BY A.ORD_NO DESC, A.SEQ ASC
			  OFFSET @P_PAGE_INDEX ROW
			  FETCH NEXT @P_TOT_PAGE_ROW ROWS ONLY
		  )AS T1 ORDER BY T1.ORD_NO DESC, T1.SEQ

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

