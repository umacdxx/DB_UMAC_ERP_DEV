
/*

-- 생성자 :	이동호
-- 등록일 :	2024.05.23
-- 수정자 : -
-- 수정일 : -
-- 설 명  : 입금내역 등록 : 토스 WebWook 통해서 가상계좌로 입금이 됐을 경우 해당 프로시저 실행
-- 실행문 : 

EXEC PR_ACCT_DEPOSIT_PUT '가상계좌', '202407052344486530', 'yumae20240705234449Z3oi8', '10000', 'DONE'

*/
CREATE PROCEDURE [dbo].[PR_ACCT_DEPOSIT_PUT]
( 
	
	@P_METHOD			NVARCHAR(30),		-- 결제 타입 : 가상계좌, 카드
	@P_MOID				NVARCHAR(100),		-- 주문번호
	@P_TID				NVARCHAR(100),		-- PG거래아이디
	@P_DEPOSIT_AMT		INT,				-- 입금금액	
	@P_VACT_STAT		NVARCHAR(30)		-- 가상계좌 결제 처리상태

)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @RETURN_CODE INT = 0									-- 리턴코드(저장완료)
	DECLARE @RETURN_MESSAGE NVARCHAR(MAX) = DBO.GET_ERR_MSG('0')	-- 리턴메시지
	DECLARE @DEPOSIT_DT VARCHAR(8) = FORMAT(GETDATE(), 'yyyyMMdd')	
	DECLARE @REQ_AMT INT = 0
	DECLARE @RECEIVE_YN VARCHAR(1) = 'N'	
	DECLARE @IDATE DATETIME = GETDATE()
	DECLARE @VEN_CODE VARCHAR(7) = '',
			@DEPOSIT_NO_CNT INT,
			@DEPOSIT_NO NVARCHAR(11),
			@DEPOSIT_GB NVARCHAR(2)			
						
	DECLARE @PA_ACCT_DEPOSIT TABLE (
			MOID NVARCHAR(100), 
			VEN_CODE NVARCHAR(7), 
			DEPOSIT_GB NVARCHAR(2),
			DEPOSIT_NO NVARCHAR(11), 
			DEPOSIT_AMT INT,
			DEPOSIT_DT NVARCHAR(8),
			DEPOSIT_FISH NVARCHAR(1),
			IDATE DATETIME	
		)
	
	SELECT @VEN_CODE = VEN_CODE FROM PA_ACCT_ISSUE WHERE MOID = @P_MOID

	IF @VEN_CODE = '' 
	BEGIN
		SET @RETURN_CODE = '-1'
		SET @RETURN_MESSAGE = '거래처 코드를 알 수 없습니다.'
		SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE
		RETURN
	END	

	BEGIN TRAN
	BEGIN TRY 			
				
		--#가상계좌 입금 결과 상태가 입금완료 일경우 Y 업데이트
		IF @P_VACT_STAT = 'DONE' 
			SET @RECEIVE_YN = 'Y'
		
		--#가상계좌 발급내역 상태 업데이트
		UPDATE PA_ACCT_ISSUE SET 
									DEPOSIT_AMT = @P_DEPOSIT_AMT,	--입금금액
									VACT_STAT = @P_VACT_STAT,		--가상계좌 결제 처리상태
									RECEIVE_YN = @RECEIVE_YN		--가상계좌입금 여부
		WHERE MOID = @P_MOID
		
		IF NOT EXISTS (SELECT 1 FROM PA_ACCT_DEPOSIT WHERE MOID = @P_MOID)
		BEGIN
			--#일자별 입금번호 순번 생성		
			SET @DEPOSIT_NO = DBO.GET_DEPOSIT_NO(@DEPOSIT_DT)
						
			INSERT INTO @PA_ACCT_DEPOSIT (
											MOID, 
											VEN_CODE,
											DEPOSIT_GB,
											DEPOSIT_NO, 
											DEPOSIT_AMT, 
											DEPOSIT_DT, 
											DEPOSIT_FISH, 
											IDATE
										)
			SELECT 
					@P_MOID, 
					@VEN_CODE, 				
					DBO.GET_TOSS_DEPOSIT_GB(@P_METHOD),
					@DEPOSIT_NO,
					@P_DEPOSIT_AMT, 
					@DEPOSIT_DT, 
					'Y',
					GETDATE()

		

			DECLARE @JSONDT NVARCHAR(MAX) = ''			
			SET @JSONDT = (
					SELECT MOID, DEPOSIT_DT, VEN_CODE, DEPOSIT_GB AS DEPOSIT_GB_NAME, DEPOSIT_AMT, DEPOSIT_NO, 'I' AS MODE FROM @PA_ACCT_DEPOSIT
						FOR JSON PATH
				)

			--#가상계좌 입금내역 테이블 등록 
			INSERT INTO PA_ACCT_DEPOSIT 
			(
				MOID, 
				VEN_CODE,
				DEPOSIT_GB,
				DEPOSIT_NO, 
				DEPOSIT_AMT, 
				DEPOSIT_DT, 
				DEPOSIT_FISH, 
				ISSUER_CODE,
				APP_NO,
				IDATE,
				IEMP_ID
			)
			SELECT MOID, VEN_CODE, DEPOSIT_GB, DEPOSIT_NO, DEPOSIT_AMT, DEPOSIT_DT, 'Y', '', '', GETDATE(), '' FROM @PA_ACCT_DEPOSIT
									
			--#주문별 입금내역 등록	
			SELECT @DEPOSIT_GB = DEPOSIT_GB FROM @PA_ACCT_DEPOSIT
						
			EXEC PR_ACCT_DEPOSIT_ORD_PUT @VEN_CODE, @DEPOSIT_GB, @P_DEPOSIT_AMT, @P_MOID, @DEPOSIT_NO, @DEPOSIT_DT, @IDATE, @RETURN_CODE OUT, @RETURN_MESSAGE OUT;
					
		END

			
		SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE

		COMMIT;

	END TRY
	BEGIN CATCH	
				
		IF @@TRANCOUNT > 0
		BEGIN 
			ROLLBACK TRAN
			
			SET @RETURN_CODE = -1
			SET @RETURN_MESSAGE = ERROR_MESSAGE()

			--에러 로그 테이블 저장
			INSERT INTO TBL_ERROR_LOG 
			SELECT ERROR_PROCEDURE()			-- 프로시저명
					, ERROR_MESSAGE()			-- 에러메시지
					, ERROR_LINE()				-- 에러라인
					, GETDATE()							
		END 

		SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE

	END CATCH
	
END

GO

