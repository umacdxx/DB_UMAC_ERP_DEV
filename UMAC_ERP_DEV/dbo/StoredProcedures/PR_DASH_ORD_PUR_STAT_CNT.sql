
/*
-- 생성자 :	최수민
-- 등록일 :	2024.07.19
-- 설 명  : 대시보드2 상단 입출고 상태별 건수 조회
			조회 기준은 입고예정일 / 출고예정일 앞뒤로 1주씩
-- 수정자 :	최수민
-- 수정일 :	2024.07.19
-- 설 명  : 
-- 실행문 : EXEC PR_DASH_ORD_PUR_STAT_CNT
*/
CREATE PROCEDURE [dbo].[PR_DASH_ORD_PUR_STAT_CNT]
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @P_START_DT		NVARCHAR(8) = CONVERT(VARCHAR(8),  DATEADD(DAY, -7, GETDATE()), 112);	-- 조회 시작일자
	DECLARE @P_END_DT		NVARCHAR(8) = CONVERT(VARCHAR(8), DATEADD(DAY, 7, GETDATE()), 112);		-- 조회 종료일자

	BEGIN TRY 

		SELECT 1 AS IO_GB
			 , '입고' AS IO_GB_NM
			 , A.CD_ID AS ORD_STAT
			 , A.CD_NM AS ORD_STAT_NM
			 , ISNULL(HDR.ORD_CNT, 0) AS ORD_CNT
		  FROM TBL_COMM_CD_MST A
		  LEFT OUTER JOIN (SELECT PUR_STAT
								, COUNT(PUR_STAT) AS ORD_CNT 
							 FROM PO_PURCHASE_HDR 
							WHERE DELIVERY_EXP_DT BETWEEN @P_START_DT AND @P_END_DT
							GROUP BY PUR_STAT) AS HDR
			ON A.CD_ID = HDR.PUR_STAT
		 WHERE A.CD_CL = 'PUR_STAT'
		   AND A.CD_ID NOT IN ('05', '15')

		UNION ALL

		SELECT 2 AS IO_GB
			 , '출고' AS IO_GB_NM
			 , A.CD_ID AS ORD_STAT
			 , A.CD_NM AS ORD_STAT_NM
			 , ISNULL(HDR.ORD_CNT, 0) AS ORD_CNT
		  FROM TBL_COMM_CD_MST A
		  LEFT OUTER JOIN (SELECT ORD_STAT
								, COUNT(ORD_STAT) AS ORD_CNT 
							 FROM PO_ORDER_HDR 
							WHERE DELIVERY_REQ_DT BETWEEN @P_START_DT AND @P_END_DT
							GROUP BY ORD_STAT) AS HDR
			ON A.CD_ID = HDR.ORD_STAT
		 WHERE A.CD_CL = 'ORD_STAT'
		   AND A.CD_ID NOT IN ('05', '15')

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

