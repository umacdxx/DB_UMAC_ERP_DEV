/*
-- 생성자 :	강세미
-- 등록일 :	2023.02.15
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 거래처 별 상위/하위배송지 리스트 출력
-- 실행문 : 
EXEC PR_PARTNER_DELIVERY_SEL_LIST 'UM20126','1260201','2' -- DELIVERY_CODE의 하위배송지 조회
EXEC PR_PARTNER_DELIVERY_SEL_LIST 'UM20126','','1' --상위배송지 전체 조회
SELECT * FROM CD_PARTNER_DELIVERY WHERE VEN_CODE = 'UM20126' AND DELIVERY_CODE LIKE '12605%'
*/
CREATE PROCEDURE [dbo].[PR_PARTNER_DELIVERY_SEL_LIST]
( 
	@P_VEN_CODE		NVARCHAR(7) = '',	-- 거래처코드
	@P_DELIVERY_CODE		NVARCHAR(7) = '',	-- 배송지코드
	@P_DELIVERY_GB		NVARCHAR(1) = ''	-- 배송지구분
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY 
	EXEC UMAC_CERT_OPEN_KEY; -- OPEN
		SELECT A.VEN_CODE
			 , A.DELIVERY_CODE
			 , A.DELIVERY_NAME
			 , A.SORT_ORDER
			 , A.DELIVERY_GB
			 , C.CD_NM AS DELIVERY_GB_NM
		     , A.POST_NO
		     , A.ADDR
		     , A.ADDR_DTL
		     , A.ADDR + ' ' + A.ADDR_DTL AS FULL_ADDR
		     , A.CHRG_NAME
		     , A.TEL_NO
			 , DBO.GET_DECRYPT(A.MOBIL_NO) AS MOBIL_NO
			 , A.EMAIL
			 , A.FAX_NO
		     , A.USE_YN
			 , B.TRANS_GB
			 , B.TRANS_SECTION
			 , D.CD_NM AS TRANS_GB_NM
			 , ISNULL((SELECT COUNT(*)-1
						 FROM CD_PARTNER_DELIVERY 
						WHERE VEN_CODE = A.VEN_CODE AND DELIVERY_CODE LIKE LEFT(A.DELIVERY_CODE, 5) + '%' ),0) AS DELIVERY_COUNT
		FROM CD_PARTNER_DELIVERY AS A WITH(NOLOCK)
			LEFT OUTER JOIN CD_DELIVERY_PRICE AS B
				  ON A.DELIVERY_PRICE_SEQ = B.SEQ
			INNER JOIN TBL_COMM_CD_MST AS C
			     ON C.CD_CL = 'DELIVERY_GB' AND C.CD_ID = A.DELIVERY_GB
			LEFT OUTER JOIN TBL_COMM_CD_MST AS D
				  ON D.CD_CL = 'TRANS_GB' AND D.CD_ID = B.TRANS_GB
	  WHERE A.VEN_CODE = @P_VEN_CODE
	    AND LEFT(A.DELIVERY_CODE,5) = (CASE WHEN @P_DELIVERY_CODE <> '' THEN LEFT(@P_DELIVERY_CODE,5) ELSE LEFT(A.DELIVERY_CODE,5) END)
	    AND 1 = (CASE WHEN @P_DELIVERY_GB = '2' THEN 1 WHEN @P_DELIVERY_GB = '1' AND A.DELIVERY_GB = @P_DELIVERY_GB THEN 1 END)
		 AND A.USE_YN = 'Y'
	EXEC UMAC_CERT_CLOSE_KEY -- CLOSE
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

