
/*
-- 생성자 :	최수민
-- 등록일 :	2024.10.07
-- 설 명  : 출고검수에서 LOT 수기입력/QR 조회 시 LOT 정보 조회
-- 수정일 : 2024.12.22 최수민 LOT_NO_TEMP에 값이 있을 시 LOT_NO_TEMP 데이터로 조회
-- 실행문 : 
EXEC PR_MO_LOT_INFO NULL,'28801052728463', '20260918'
*/
CREATE PROCEDURE [dbo].[PR_MO_LOT_INFO]
( 
	@P_LOT_NO			NVARCHAR(30) = '',		-- LOT_NO
	@P_SCAN_CODE		NVARCHAR(14) = '',		-- 상품코드(상세정보 출력)
	@P_EXPIRY_DATE		NVARCHAR(8) = ''		-- 소비기한
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 

		IF ISNULL(@P_LOT_NO, '') = ''
		BEGIN 

			SELECT IIF(BOX.BOX_CODE = NULL, LOT.SCAN_CODE, CMN.SCAN_CODE) AS SCAN_CODE
				 , LOT.LOT_NO
				 , LOT.EXPIRATION_DT
				 , 0 AS PICKING_QTY
				 , STAT.CUR_INV_QTY
			  FROM CD_PRODUCT_CMN AS CMN
	 		  LEFT OUTER JOIN CD_BOX_MST AS BOX ON CMN.ITM_CODE = BOX.BOX_CODE
			  LEFT OUTER JOIN CD_PRODUCT_CMN AS CHILD_CMN ON BOX.ITM_CODE = CHILD_CMN.ITM_CODE
			  LEFT JOIN VIEW_CD_LOT_MST AS LOT ON ISNULL(CHILD_CMN.SCAN_CODE, CMN.SCAN_CODE) = LOT.SCAN_CODE
			  LEFT JOIN IV_LOT_STAT AS STAT ON LOT.LOT_NO = STAT.LOT_NO AND CMN.SCAN_CODE = STAT.SCAN_CODE
			 WHERE CMN.SCAN_CODE = @P_SCAN_CODE
			   AND LOT.EXPIRATION_DT = @P_EXPIRY_DATE
			 ORDER BY CMN.SCAN_CODE

		END
		ELSE
		BEGIN

			IF EXISTS (
				SELECT 1 
				  FROM CD_LOT_MST 
				 WHERE LOT_NO_TEMP = @P_LOT_NO
			)
			BEGIN
				SELECT VLOT.SCAN_CODE
					 , VLOT.LOT_NO
					 , VLOT.EXPIRATION_DT
					 , 0 AS PICKING_QTY
					 , 0.0 AS CUR_INV_QTY
				  FROM CD_LOT_MST AS LOT
				  LEFT OUTER JOIN VIEW_CD_LOT_MST AS VLOT ON LOT.LOT_NO = VLOT.LOT_NO
				 WHERE LOT.LOT_NO_TEMP = @P_LOT_NO
			END
			ELSE
			BEGIN
				SELECT SCAN_CODE
					 , LOT_NO
					 , EXPIRATION_DT
					 , 0 AS PICKING_QTY
					 , 0.0 AS CUR_INV_QTY
				  FROM VIEW_CD_LOT_MST
				 WHERE LOT_NO = @P_LOT_NO
			END

		END
					
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

