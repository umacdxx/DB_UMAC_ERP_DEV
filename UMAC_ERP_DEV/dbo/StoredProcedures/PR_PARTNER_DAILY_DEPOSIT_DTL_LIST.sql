/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.06.17
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 
-- 실행문 : EXEC PR_PARTNER_DAILY_DEPOSIT_DTL_LIST '20240625','2240625003','UM20128'
*/
CREATE PROCEDURE [dbo].[PR_PARTNER_DAILY_DEPOSIT_DTL_LIST]
( 
	@P_SALE_DT		NVARCHAR(8) = '',	-- 매출일자
	@P_ORD_NO		NVARCHAR(11) = '',	-- 주문번호
	@P_VEN_CODE		NVARCHAR(7) = ''	-- 거래처코드
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
	
		EXEC UMAC_CERT_OPEN_KEY; -- OPEN
				
		SELECT A.DEPOSIT_AMT
			 , B.DEPOSIT_DT
			 , B.DEPOSIT_GB
			 , E.CD_NM AS DEPOSIT_GB_NAME
			 , (CASE WHEN B.DEPOSIT_GB = '01' THEN ISNULL(DBO.GET_DECRYPT(D.VACT_NO), '미발행') WHEN B.DEPOSIT_GB = '02' THEN G.CRYP_CMSV_ACCT_NO ELSE '-' END) AS VACT_NO			
			 , D.BANK_CODE
			 , (CASE WHEN B.DEPOSIT_GB = '01' THEN F.CD_NM WHEN B.DEPOSIT_GB = '02' THEN GCD.CD_DESCRIPTION ELSE '-' END) AS BANK_NAME
			 , B.DEPOSIT_NO
			 , '' AS CARD_NO
			 , B.APP_NO
			 , B.ISSUER_CODE
			 , TCD.CD_NM AS ISSUER_CODE_NM			 
			 , FORMAT(B.IDATE, 'yyyy/MM/dd HH:mm') AS IDATE
			FROM PA_ACCT_DEPOSIT_ORD AS A
				INNER JOIN PA_ACCT_DEPOSIT AS B ON A.VEN_CODE = B.VEN_CODE AND A.DEPOSIT_NO = B.DEPOSIT_NO
				LEFT OUTER JOIN PA_ACCT_ISSUE AS C ON B.MOID = C.MOID
				LEFT OUTER JOIN PA_ACCT_MST AS D ON B.VEN_CODE = D.VEN_CODE
				LEFT OUTER JOIN TBL_COMM_CD_MST AS E ON E.CD_CL = 'DEPOSIT_GB' AND B.DEPOSIT_GB = E.CD_ID
				LEFT OUTER JOIN TBL_COMM_CD_MST AS F ON F.CD_CL = 'TOSS_BANK' AND D.BANK_CODE = F.CD_ID
				LEFT OUTER JOIN TBL_COMM_CD_MST AS TCD ON TCD.CD_CL = 'TOSS_CARD_XPAY' AND TCD.CD_ID = B.ISSUER_CODE
				LEFT OUTER JOIN HCMS_ACCT_TRSC_PTCL AS G ON B.MOID = G.MOID
				LEFT OUTER JOIN TBL_COMM_CD_MST AS GCD ON GCD.CD_CL = 'GENERAL_ACC_NO_LIST' AND GCD.CD_NM = G.CRYP_CMSV_ACCT_NO
		   WHERE A.SALE_DT = @P_SALE_DT
			 AND A.ORD_NO = @P_ORD_NO
		     AND A.VEN_CODE = @P_VEN_CODE
			 AND A.DEPOSIT_AMT > 0
		   ORDER BY B.DEPOSIT_DT, B.DEPOSIT_GB, B.DEPOSIT_NO
		   
		EXEC UMAC_CERT_CLOSE_KEY; -- CLOSE
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

