/*
-- 생성자 :	강세미
-- 등록일 :	2023.03.05
-- 수정자 : 2024.07.31 강세미 - 벌크제품 현재고, 가용재고 0 처리
-- 설 명  : 발주등록 DTL 조회
-- 실행문 : 
EXEC [PR_PURCHASE_DTL_LIST] '1240611003'
*/
CREATE PROCEDURE [dbo].[PR_PURCHASE_DTL_LIST]
( 
	@P_ORD_NO	NVARCHAR(11) = ''   -- 발주번호
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY 
		SELECT ROW_NUMBER() OVER(ORDER BY SEQ) AS ROW_NUM,
				 A.ORD_NO,
				 A.SEQ,
				 A.SCAN_CODE,
				 A.ORD_QTY,
				 A.ORD_WPRC,
				 A.ORD_WVAT,
				 A.ORD_TOTAL_WPRC,
				 A.ORD_WAMT,
				 A.REMARKS,
				 B.ITM_NAME,
				 B.ITM_NAME_DETAIL,
				 B.UNIT_CAPACITY,
				 UCOM.CD_NM AS UNIT_NM,
				 B.WEIGHT_GB,
				 C.CD_NM AS TAX_GB_NM,
				 CASE WHEN B.ITM_FORM = '3' THEN 0 WHEN B.ITM_FORM = '2' THEN E.BOX_EACH_QTY ELSE E.CUR_INV_QTY END AS CUR_INV_QTY,
				 CASE WHEN B.ITM_FORM = '3' THEN 0 WHEN B.ITM_FORM = '2' THEN E.BOX_EACH_QTY - ISNULL(H.TOTAL_ORD_QTY, 0) ELSE E.CUR_INV_QTY - ISNULL(H.TOTAL_ORD_QTY, 0) END AS AVL_INV_QTY
		  FROM PO_PURCHASE_DTL AS A
				INNER JOIN CD_PRODUCT_CMN AS B
						ON B.SCAN_CODE = A.SCAN_CODE
				INNER JOIN TBL_COMM_CD_MST AS C
						ON C.CD_CL = 'TAX_GB' AND A.TAX_GB = C.CD_ID
				LEFT OUTER JOIN TBL_COMM_CD_MST AS UCOM
						ON UCOM.CD_CL = 'UNIT' AND B.UNIT = UCOM.CD_ID
				LEFT OUTER JOIN IV_PRODUCT_STAT AS D 
						ON A.SCAN_CODE = D.SCAN_CODE
				LEFT OUTER JOIN (SELECT A.SCAN_CODE
								, A.CUR_INV_QTY
								, C.CUR_INV_QTY AS BOX_EACH_QTY
							FROM IV_PRODUCT_STAT AS A
							LEFT OUTER JOIN CD_BOX_MST AS B ON A.ITM_CODE = B.BOX_CODE
							LEFT OUTER JOIN IV_PRODUCT_STAT AS C ON B.ITM_CODE = C.ITM_CODE
								) AS E
					ON A.SCAN_CODE = E.SCAN_CODE
		 LEFT OUTER JOIN VIEW_TOTAL_ORDER_QTY AS H ON B.ITM_CODE = H.ITM_CODE
		 WHERE A.ORD_NO = @P_ORD_NO

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

