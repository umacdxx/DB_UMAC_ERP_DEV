/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.04.11
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 생산재고 DTL 조회
-- 실행문 : EXEC PR_PROD_STOCK_REGISTER_DTL_LIST  
*/
CREATE PROCEDURE [dbo].[PR_PROD_STOCK_REGISTER_DTL_LIST]
	@P_PROD_DT			NVARCHAR(10),	-- 생산일자
	@P_SCAN_CODE		NVARCHAR(14),	-- 상품코드
	@P_PROD_GB			NVARCHAR(1),	-- 생산구분
	@P_PROD_GB_CD		NVARCHAR(3),	-- 생산구분코드
	@P_LOT_NO			NVARCHAR(30)	-- LOT_NO
AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 
		
		IF @P_PROD_GB = 'B'	-- BOM 조회
		BEGIN
			SELECT ROW_NUMBER() OVER(ORDER BY C.BOM_COMP_CD) AS ROW_NUM
			     , A.PROD_DT 
			     , C.BOM_COMP_CD 			     
				 , D.ITM_NAME_DETAIL AS BOM_COMP_NM
				 , F.LRG_NAME
				 , E.MID_NAME
				 , A.PROD_QTY * C.COMP_QTY AS PROD_DTL_QTY
				 , A.PROD_APP_QTY * C.COMP_QTY AS PROD_APP_DTL_QTY
				 --, D.NET_WEIGHT * (A.PROD_QTY * C.COMP_QTY) AS WEIGHT
				 , CASE WHEN D.WEIGHT_GB = 'WT' THEN A.PROD_APP_QTY * C.COMP_QTY 
				        --ELSE (D.NET_WEIGHT / 1000) * (A.PROD_QTY * C.COMP_QTY) END AS WEIGHT
				        ELSE (CAST(D.NET_WEIGHT AS DECIMAL(15, 2)) / 1000) * (A.PROD_APP_QTY * C.COMP_QTY) END AS WEIGHT
						
				 , G.USER_NM AS IEMP_ID
				 , CONVERT(NVARCHAR(8), A.IDATE, 112) AS IDATE
				 , D.WEIGHT_GB
				FROM CD_LOT_MST AS A
				INNER JOIN CD_BOM_HDR 		AS B ON A.PROD_GB_CD = B.BOM_CD AND A.SCAN_CODE = B.BOM_PROD_CD
				INNER JOIN CD_BOM_DTL 		AS C ON B.BOM_CD = C.BOM_CD 
				INNER JOIN CD_PRODUCT_CMN 	AS D ON C.BOM_COMP_CD = D.SCAN_CODE
				INNER JOIN CD_MID_MST 		AS E ON D.MID_CODE  = E.MID_CODE
				INNER JOIN CD_LRG_MST 		AS F ON E.LRG_CODE  = F.LRG_CODE
				INNER JOIN TBL_USER_MST		AS G ON A.IEMP_ID = G.USER_ID
			   WHERE 1=1
			     AND A.PROD_DT 		= @P_PROD_DT
			     AND A.SCAN_CODE 	= @P_SCAN_CODE
				 AND A.LOT_NO		= @P_LOT_NO
			     AND A.PROD_GB 		= @P_PROD_GB
			     AND B.BOM_CD 		= @P_PROD_GB_CD
		END
		
		
		ELSE IF @P_PROD_GB = 'S' -- SET 조회
		BEGIN
			SELECT ROW_NUMBER() OVER(ORDER BY C.SET_COMP_CD) AS ROW_NUM
			     , A.PROD_DT 
			     , C.SET_COMP_CD 
			     , D.ITM_NAME_DETAIL AS SET_COMP_NM				 				 
				 , F.LRG_NAME
				 , E.MID_NAME
				 , A.PROD_QTY * C.COMP_QTY AS PROD_DTL_QTY
				 , A.PROD_APP_QTY * C.COMP_QTY AS PROD_APP_DTL_QTY
				 , D.NET_WEIGHT * (A.PROD_QTY * C.COMP_QTY) AS WEIGHT
				 , A.IEMP_ID
				 , CONVERT(NVARCHAR(8), A.IDATE, 112) AS IDATE
				 , D.WEIGHT_GB
				FROM CD_LOT_MST AS A
				INNER JOIN CD_SET_HDR 		AS B ON A.PROD_GB_CD = B.SET_CD AND A.SCAN_CODE = B.SET_PROD_CD
				INNER JOIN CD_SET_DTL 		AS C ON B.SET_CD = C.SET_CD 
				INNER JOIN CD_PRODUCT_CMN 	AS D ON C.SET_COMP_CD = D.SCAN_CODE
				INNER JOIN CD_MID_MST 		AS E ON D.MID_CODE  = E.MID_CODE
				INNER JOIN CD_LRG_MST 		AS F ON E.LRG_CODE  = F.LRG_CODE
			   WHERE 1=1
			     AND A.PROD_DT 		= @P_PROD_DT
			     AND A.SCAN_CODE 	= @P_SCAN_CODE
			     AND A.PROD_GB 		= @P_PROD_GB
			     AND B.SET_CD 		= @P_PROD_GB_CD
		END
		
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

