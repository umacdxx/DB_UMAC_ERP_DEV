/*
-- 생성자 :	강세미
-- 등록일 :	2024.04.12
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 주문별 합계
-- 실행문 : EXEC PR_ORDER_SUM '2240808001'
*/
CREATE PROCEDURE [dbo].[PR_ORDER_SUM]
	@P_ORD_NO NVARCHAR(11),		-- 주문번호
	@P_ORD_STAT NVARCHAR(3)		-- 주문상태
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	  
	BEGIN TRY 
		SELECT
			TAX_GB,
			COUNT(CASE WHEN TAX_GB = '1' THEN 1 END) AS TAX_ITM_QTY,							-- 과세 상품건수
			COUNT(CASE WHEN TAX_GB = '2' THEN 1 END) AS TAX_FREE_ITM_QTY,						-- 면세 상품건수
			COUNT(*) AS TOT_ITM_QTY,													-- 총 상품건수
			CAST(SUM(CASE WHEN TAX_GB = '1' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_QTY ELSE ISNULL(PICKING_QTY,0) END ELSE 0 END) AS DECIMAL(15,2)) AS TAX_PICKING_QTY,			-- 과세 출고량
			CAST(SUM(CASE WHEN TAX_GB = '2' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_QTY ELSE ISNULL(PICKING_QTY,0) END ELSE 0 END) AS DECIMAL(15,2)) AS TAX_FREE_PICKING_QTY,	-- 면세 출고량
			CAST(ISNULL(SUM(CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_QTY ELSE PICKING_QTY END),0) AS DECIMAL(15,2)) AS TOT_PICKING_QTY,												-- 총 출고량
			SUM(CASE WHEN TAX_GB = '1' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SPRC ELSE ISNULL(PICKING_SPRC,0) END ELSE 0 END) AS TAX_PICKING_SPRC,		-- 과세 공급가
			SUM(CASE WHEN TAX_GB = '2' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SPRC ELSE ISNULL(PICKING_SPRC,0) END ELSE 0 END) AS TAX_FREE_PICKING_SPRC,	-- 면세 공급가
			ISNULL(SUM(CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SPRC ELSE PICKING_SPRC END),0) AS TOT_PICKING_SPRC,												-- 총 공급가
			SUM(CASE WHEN TAX_GB = '1' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SVAT ELSE ISNULL(PICKING_SVAT,0) END ELSE 0 END) AS TAX_PICKING_SVAT,		-- 과세 부가세
			SUM(CASE WHEN TAX_GB = '2' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SVAT ELSE ISNULL(PICKING_SVAT,0) END ELSE 0 END) AS TAX_FREE_PICKING_SVAT,	-- 면세 부가세
			ISNULL(SUM(CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SVAT ELSE PICKING_SVAT END),0) AS TOT_PICKING_SVAT,												-- 총 부가세
			SUM(CASE WHEN TAX_GB = '1' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SAMT ELSE ISNULL(PICKING_SAMT,0) END ELSE 0 END) AS TAX_PICKING_SAMT,		-- 과세 합계
			SUM(CASE WHEN TAX_GB = '2' THEN CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SAMT ELSE ISNULL(PICKING_SAMT,0) END ELSE 0 END) AS TAX_FREE_PICKING_SAMT,	-- 면세 합계
			ISNULL(SUM(CASE WHEN @P_ORD_STAT IN ('10', '25') THEN ORD_SAMT ELSE PICKING_SAMT END),0) AS TOT_PICKING_SAMT												-- 총 합계
			FROM VIEW_ORDER_DTL_SAMPLE_SUM AS A
			INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
			WHERE ORD_NO = @P_ORD_NO
			GROUP BY TAX_GB;

	END TRY

	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
 
	END CATCH 
END

GO

