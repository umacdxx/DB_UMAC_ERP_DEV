/*
-- 생성자 :	강세미
-- 등록일 :	2024.02.15
-- 수정자 : 2024.12.06 강세미- 거래구분 컬럼 조회 추가
-- 설 명  : 주문등록 HDR 조회
-- 실행문 : 
EXEC PR_ORDER_HDR_INFO '2240624001'
*/
CREATE PROCEDURE [dbo].[PR_ORDER_HDR_INFO]
( 
	@P_ORD_NO	NVARCHAR(11) = ''   -- 주문번호
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY 
	SELECT A.ORD_NO,
				A.ORD_DT,
				A.ORD_AMT,
				A.PICKING_TOTAL_AMT,
				A.VEN_CODE,
				A.DELIVERY_REQ_DT,
				A.DELIVERY_DEC_DT,
				A.ARRIVAL_REQ_DT,
				A.DELIVERY_CODE,
				A.R_POST_NO,
				A.R_ADDR,
				A.R_ADDR_DTL,
				A.ORD_STAT,
				A.ORD_CFM,
				A.TRANS_YN,
				A.REMARKS,
				A.DOCUMENT_REQ_1,
				A.DOCUMENT_REQ_2,
				A.DOCUMENT_REQ_3,
				A.DOCUMENT_REQ_4,
				A.DOCUMENT_REMARKS,
				B.VEN_NAME,				
				B.VEN_GB,
				ISNULL(B.CREDIT_LIMIT,0) AS CREDIT_LIMIT,
				C.DELIVERY_GB,
				C.DELIVERY_NAME,
				D.CD_NM AS ORD_STAT_NM,
				ISNULL(B.CREDIT_LIMIT - (E.SALE_TOTAL_AMT - ISNULL(F.DEPOSIT_AMT, 0)),0) AS CREDIT_BALANCE
		FROM PO_ORDER_HDR AS A
		INNER JOIN CD_PARTNER_MST AS B
					ON A.VEN_CODE = B.VEN_CODE
		LEFT OUTER JOIN CD_PARTNER_DELIVERY AS C
					ON A.VEN_CODE = C.VEN_CODE AND C.DELIVERY_CODE = A.DELIVERY_CODE
		INNER JOIN TBL_COMM_CD_MST AS D
					ON D.CD_CL = 'ORD_STAT' AND A.ORD_STAT = D.CD_ID
		LEFT OUTER JOIN (SELECT A.VEN_CODE,
							    SUM(A.SALE_TOTAL_AMT) AS SALE_TOTAL_AMT
						   FROM SL_SALE_VEN AS A
					   GROUP BY A.VEN_CODE 
					   ) AS E ON A.VEN_CODE = E.VEN_CODE
		LEFT OUTER JOIN (SELECT A.VEN_CODE,
								SUM(A.DEPOSIT_AMT) AS DEPOSIT_AMT
						   FROM PA_ACCT_DEPOSIT AS A
						  WHERE DEPOSIT_FISH = 'Y'
					   GROUP BY A.VEN_CODE
					   ) AS F ON A.VEN_CODE = F.VEN_CODE
		
		WHERE A.ORD_NO = @P_ORD_NO
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

