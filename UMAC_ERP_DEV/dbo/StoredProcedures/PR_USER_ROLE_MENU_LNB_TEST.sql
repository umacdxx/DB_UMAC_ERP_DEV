/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.02.21
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 메뉴 경로 위한 테스트 프로시저 (*나중에 삭제 예정*)
-- 실행문 : 
EXEC PR_USER_ROLE_MENU_LNB_TEST 'admin'
*/
CREATE PROCEDURE [dbo].[PR_USER_ROLE_MENU_LNB_TEST]
	@P_USER_ID NVARCHAR(20) = '' -- 사용자ID
AS
BEGIN
   SET NOCOUNT ON;
   SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 
		WITH TEMP_TBL
		AS (
			SELECT 							
				A.SEQ,
				A.MENU_CODE,
				A.MENU_NM,			
				A.MENU_GB,
				A.MENU_ICON,
				A.MENU_LINK,
				A.UP_MENU_CODE,
				A.SORT_ORDER,
				A.HELP,
				A.DEL_YN,
				CONVERT(VARCHAR(50), A.MENU_CODE) AS SORT,
				CONVERT(VARCHAR(50), A.MENU_NM) AS MENU_NM_PATH
							
			FROM TBL_MENU_INFO AS A
			WHERE  UP_MENU_CODE = ''
			UNION ALL
			SELECT 
				DM.SEQ,
				DM.MENU_CODE,
				DM.MENU_NM,			
				DM.MENU_GB,
				DM.MENU_ICON,
				DM.MENU_LINK,
				DM.UP_MENU_CODE,
				DM.SORT_ORDER,
				DM.HELP,
				DM.DEL_YN,		
				CONVERT(VARCHAR(50), CONVERT(NVARCHAR, TT.SORT) + N' > ' + CONVERT(VARCHAR(255), dbo.FN_DEPT_CODE_SORT_CH(DM.MENU_CODE,DM.SORT_ORDER))) SORT,
				CONVERT(VARCHAR(50), CONVERT(NVARCHAR, TT.MENU_NM_PATH) + N' > ' + CONVERT(VARCHAR(255), DM.MENU_NM)) MENU_NM_PATH
			FROM TBL_MENU_INFO AS DM,
				TEMP_TBL AS TT
			WHERE DM.UP_MENU_CODE = TT.MENU_CODE
		)
		SELECT B.SEQ
			  , A.MENU_CODE
			  , B.MENU_NM
			  , B.MENU_GB
			  , B.MENU_ICON
			  , B.MENU_LINK
			  , B.UP_MENU_CODE
			  , B.SORT_ORDER
			  , B.HELP
			  , B.DEL_YN
			  , '' AS LOW_MENU_CODE 
			  , 0 AS LOW_SORT_ORDER
			  , '' AS UPPER_MENU_NM
			  , C.MENU_NM_PATH
		  FROM TBL_USER_ROLE_MST AS A
				INNER JOIN TBL_MENU_INFO AS B
					ON A.MENU_CODE = B.MENU_CODE
				INNER JOIN TEMP_TBL AS C
					ON A.MENU_CODE = C.MENU_CODE
		 WHERE A.USER_ID = @P_USER_ID
		   AND B.DEL_YN = 'N'

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH

END

GO

