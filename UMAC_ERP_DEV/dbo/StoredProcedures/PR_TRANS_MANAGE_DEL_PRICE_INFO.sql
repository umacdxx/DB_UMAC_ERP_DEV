/*
-- 생성자 :	강세미
-- 등록일 :	2023.03.18
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 배차관리 차량구분에 따른 운송비, 용차비 리스트
-- 실행문 : 
EXEC PR_TRANS_MANAGE_DEL_PRICE_INFO 'UM20126','1260101', '1', '2'
*/
CREATE PROCEDURE [dbo].[PR_TRANS_MANAGE_DEL_PRICE_INFO]
( 
	@P_VEN_CODE		NVARCHAR(7) = '',		  -- 거래처코드
	@P_DELIVERY_CODE	NVARCHAR(7) = '',	  -- 배송지코드
	@P_CAR_GB	NVARCHAR(2) = '',			  -- 차량구분
	@P_SEQ	INT			  -- SEQ
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY
		 WITH W_UNPIVOT(TRANS_GB, TRANS_SECTION, TRANS_COST_1, RENT_COST_1, TRANS_COST_2, RENT_COST_2, TRANS_COST_3, RENT_COST_3, 
							 TRANS_COST_4, RENT_COST_4, TRANS_COST_5, RENT_COST_5, SEQ) 
		 AS (
			 SELECT TRANS_GB,
					  TRANS_SECTION,
					  TRANS_COST_1,
					  RENT_COST_1,
					  TRANS_COST_2,
					  RENT_COST_2,
					  TRANS_COST_3,
					  RENT_COST_3,
					  TRANS_COST_4,
					  RENT_COST_4,
					  TRANS_COST_5,
					  RENT_COST_5,
					  SEQ
			   FROM CD_DELIVERY_PRICE AS A
					LEFT OUTER JOIN CD_PARTNER_DELIVERY AS B
					ON B.VEN_CODE = @P_VEN_CODE AND B.DELIVERY_CODE = @P_DELIVERY_CODE
			   WHERE 1 =  (CASE WHEN @P_SEQ = '' AND  B.DELIVERY_PRICE_SEQ = A.SEQ THEN 1 WHEN @P_SEQ <> '' THEN 1 ELSE 0 END) 	
					AND 1 = (CASE WHEN @P_SEQ = '' THEN 1 WHEN @P_SEQ <> '' AND A.SEQ = @P_SEQ THEN 1 ELSE 0 END) 	
		 )
		 SELECT TRANS_GB,
				TRANS_SECTION,
				TRANS_COST_GB,
				COST,
				SEQ
			FROM W_UNPIVOT
				  UNPIVOT (COST FOR TRANS_COST_GB IN (TRANS_COST_1, RENT_COST_1, TRANS_COST_2, RENT_COST_2, TRANS_COST_3, RENT_COST_3, 
							   TRANS_COST_4, RENT_COST_4, TRANS_COST_5, RENT_COST_5)) AS W
		  WHERE RIGHT(TRANS_COST_GB, 1) = @P_CAR_GB
		
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

