/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.08.13
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : SET 작업 조회, 자재 환원 조회
-- 실행문 : EXEC PR_SET_PROD_RESTORE_REGISTER_LIST '','',''
*/
CREATE PROCEDURE [dbo].[PR_SET_PROD_RESTORE_REGISTER_LIST]
( 
	@P_SET_PLAN_ID			NVARCHAR(11) = '',		-- 작업코드

	-- 해당하는 작업코드 모두 조회, 일자는 일단 안걸어놓음
	@P_SET_PROD_SDATE		NVARCHAR(8) = '',		-- 생산시작일자
	@P_SET_PROD_EDATE		NVARCHAR(8) = ''		-- 생산종료일자
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
		WITH W_RESTORE AS (
			SELECT A.SEQ
			     , A.SET_COMP_CD
				 , A.COMP_QTY
				 , A.LOT_NO
				 , MST_USR.USER_NM
				FROM PD_SET_RESULT_COMP AS A
				INNER JOIN TBL_USER_MST AS MST_USR ON A.IEMP_ID = MST_USR.USER_ID
			   WHERE A.RESTORE_YN = 'Y'
				 AND A.SET_PLAN_ID = @P_SET_PLAN_ID
				 AND A.SET_COMP_CD IN (
					SELECT DISTINCT SET_COMP_CD 
						FROM PD_SET_RESULT_COMP
					   WHERE RESTORE_YN = 'Y'
						 AND SET_PLAN_ID = @P_SET_PLAN_ID
				 )
		), W_COMP AS (
			SELECT A.SET_COMP_CD
				 , A.COMP_QTY
				 , A.LOT_NO
				 --, SUM(A.COMP_QTY) OVER (PARTITION BY A.SET_COMP_CD) AS COMP_SUM
				FROM PD_SET_RESULT_COMP AS A
			   WHERE A.RESTORE_YN = 'N'
				 AND SET_PLAN_ID = @P_SET_PLAN_ID
		), W_COMP_SUM AS (
			SELECT A.SET_COMP_CD
				 , SUM(A.COMP_QTY) AS COMP_SUM
				FROM PD_SET_RESULT_COMP AS A
			   WHERE A.RESTORE_YN = 'N'
				 AND SET_PLAN_ID = @P_SET_PLAN_ID
			   GROUP BY A.SET_COMP_CD
		) , W_MAIN AS (
			SELECT A.SEQ
				 , A.SET_COMP_CD AS SCAN_CODE
				 , MST_PRD.ITM_NAME_DETAIL AS ITM_NAME
				 , C.COMP_SUM
				 , A.LOT_NO
				 , ISNULL(B.COMP_QTY, 0) AS COMP_QTY
				 , A.COMP_QTY AS RESTORE_QTY
				 , A.USER_NM
				 , DENSE_RANK() OVER(ORDER BY A.SET_COMP_CD) AS MERGE_INDEX
				 --, DENSE_RANK() OVER(ORDER BY A.SET_COMP_CD, B.COMP_SUM) AS MERGE_INDEX
				FROM W_RESTORE AS A
				LEFT OUTER JOIN W_COMP AS B ON A.SET_COMP_CD = B.SET_COMP_CD AND A.LOT_NO = B.LOT_NO
				INNER JOIN W_COMP_SUM AS C ON A.SET_COMP_CD = C.SET_COMP_CD
				INNER JOIN CD_PRODUCT_CMN AS MST_PRD ON A.SET_COMP_CD = MST_PRD.SCAN_CODE
		)
		SELECT A.*
		     , COUNT(*) OVER(PARTITION BY MERGE_INDEX) AS MERGE_CNT
			FROM W_MAIN AS A


	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

