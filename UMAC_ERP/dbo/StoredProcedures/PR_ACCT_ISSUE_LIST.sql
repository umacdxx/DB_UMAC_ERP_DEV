
/*
-- 생성자 :	이동호
-- 등록일 :	2024.06.03
-- 수정자 : 최수민
-- 수정일 : 2024.08.23 - 거래처 담당자 추가
-- 설 명  : 거래처 가상계좌 발급내역 (대금정산 계좌 리스트)
-- 실행문 : 
	
EXEC PR_ACCT_ISSUE_LIST '','2023-01-05','2024-07-26'

*/
CREATE PROCEDURE [dbo].[PR_ACCT_ISSUE_LIST]
( 
	@P_VEN_CODE		NVARCHAR(7) = '',	-- 거래처코드
	@P_SALE_DT_ST	NVARCHAR(10) = '',	-- 조회일자
	@P_SALE_DT_ED	NVARCHAR(10) = '',	-- 조회일자
	@P_EMP_NAME		NVARCHAR(10) = ''	-- 거래처담당자명
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	 
	BEGIN TRY 
		
		SET @P_SALE_DT_ST = REPLACE(@P_SALE_DT_ST, '-','')
		SET @P_SALE_DT_ED = REPLACE(@P_SALE_DT_ED, '-','')
		
		EXEC UMAC_CERT_OPEN_KEY; -- OPEN
		
		SELECT 			
			PMT.VEN_CODE,																		--거래처코드
			PMT.VEN_NAME,																		--거래처이름
			ISNULL(T1.SALE_TOTAL_AMT, 0) AS SALE_TOTAL_AMT,										--매출금액
			ISNULL(RECEIVABLE_AMT, 0) AS TOTAL_AMT,												--현재 잔액 (매출금액 - 입금금맥)
			ISNULL(AMST.BANK_CODE, '') AS BANK_CODE,											--은행코드
			ISNULL(DBO.GET_DECRYPT(AMST.VACT_NO), '') AS VACT_NO,								--가상계좌번호
			ISNULL(COM.CD_NM, '') AS BANK_NM													--은행이름
		FROM CD_PARTNER_MST AS PMT 
			LEFT OUTER JOIN
			(									
				SELECT 				
					VEN_CODE,
					SUM(SALE_TOTAL_AMT) AS SALE_TOTAL_AMT,
					SUM(CASE WHEN DEPOSIT_AMT < 0 THEN DEPOSIT_AMT ELSE SALE_TOTAL_AMT - DEPOSIT_AMT END) AS RECEIVABLE_AMT
				FROM PA_ACCT_DEPOSIT_ORD 						
				WHERE 1 = (CASE WHEN @P_VEN_CODE <> '' AND VEN_CODE = @P_VEN_CODE THEN 1 WHEN @P_VEN_CODE = '' THEN 1 ELSE 0 END )
					AND 1 = (CASE WHEN @P_SALE_DT_ST <> '' AND @P_SALE_DT_ED <> '' AND SALE_DT BETWEEN @P_SALE_DT_ST AND @P_SALE_DT_ED THEN 1 WHEN @P_SALE_DT_ST = '' AND @P_SALE_DT_ED = '' THEN 1 ELSE 0 END )					
				GROUP BY VEN_CODE
			
			) AS T1 ON PMT.VEN_CODE = T1.VEN_CODE			
			LEFT OUTER JOIN PA_ACCT_MST AS AMST ON PMT.VEN_CODE = AMST.VEN_CODE
			LEFT OUTER JOIN TBL_COMM_CD_MST AS COM ON COM.CD_CL='TOSS_BANK' AND COM.CD_ID = AMST.BANK_CODE
			LEFT OUTER JOIN CD_PARTNER_EMP AS EMP ON PMT.VEN_CODE = EMP.VEN_CODE
		WHERE 1 = (CASE WHEN @P_VEN_CODE <> '' AND PMT.VEN_CODE = @P_VEN_CODE THEN 1 WHEN @P_VEN_CODE = '' THEN 1 ELSE 0 END )
			AND PMT.USE_YN = 'Y'
			AND 1 = (CASE WHEN @P_EMP_NAME <> '' AND USER_NM = @P_EMP_NAME THEN 1 WHEN @P_EMP_NAME = '' THEN 1 ELSE 0 END ) 
		GROUP BY 
			PMT.VEN_CODE, 
			PMT.VEN_NAME, 
			T1.SALE_TOTAL_AMT, 
			T1.RECEIVABLE_AMT, 
			AMST.BANK_CODE, 
			DBO.GET_DECRYPT(AMST.VACT_NO), 
			COM.CD_NM
		ORDER BY CASE WHEN RECEIVABLE_AMT <> 0 THEN 0 ELSE 1 END, PMT.VEN_CODE;


		EXEC UMAC_CERT_CLOSE_KEY; -- CLOSE

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

