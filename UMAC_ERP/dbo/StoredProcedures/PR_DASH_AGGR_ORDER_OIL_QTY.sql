
/*
-- 생성자 :	최수민
-- 등록일 :	2024.08.23
-- 설 명  : 대시보드 유종별 출고량
-- 수정자 :	-
-- 수정일 :	-
-- 설 명  : 
-- 실행문 : EXEC PR_DASH_AGGR_ORDER_OIL_QTY '20240801' ,'20240831', 'DW,DS'
			EXEC PR_DASH_AGGR_ORDER_OIL_QTY '20240801' ,'20240831', NULL
*/
CREATE PROCEDURE [dbo].[PR_DASH_AGGR_ORDER_OIL_QTY]
(
	@P_START_DT				NVARCHAR(8) = '',			-- 조회시작일자
	@P_END_DT				NVARCHAR(8) = '',			-- 조회종료일자
	@P_LOT_PARTNER_GB	    NVARCHAR(25) = ''			-- LOT 거래처 구분
)
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;


	BEGIN TRY 

			
		WITH CURR_DATA AS (
			SELECT CDMST.SORT_ORDER
				 , CDMST.CD_ID AS LOT_OIL_GB
				 , CDMST.CD_NM AS LOT_OIL_GB_NM
				 , ISNULL(SUM(AO.AGGR_KG_QTY), 0) AS AGGR_SUM1
			  FROM AG_ORDER_OIL AS AO
			 INNER JOIN TBL_COMM_CD_MST AS CDMST ON CDMST.CD_CL = 'LOT_OIL_GB' AND AO.LOT_OIL_GB = CDMST.CD_ID
			 WHERE ISNULL(AO.AGGR_DT, '') BETWEEN @P_START_DT AND @P_END_DT
			   AND (
						ISNULL(@P_LOT_PARTNER_GB, '') = '' 
						OR AO.LOT_PARTNER_GB IN (
							SELECT value FROM STRING_SPLIT(@P_LOT_PARTNER_GB, ',')
						)
					)
			 GROUP BY CDMST.SORT_ORDER, CDMST.CD_ID, CDMST.CD_NM
		), PREV_DATA AS (
			SELECT CDMST.SORT_ORDER
				 , CDMST.CD_ID AS LOT_OIL_GB
				 , CDMST.CD_NM AS LOT_OIL_GB_NM
				 , ISNULL(SUM(AO.AGGR_KG_QTY), 0) AS AGGR_SUM2
			  FROM AG_ORDER_OIL AS AO
			 INNER JOIN TBL_COMM_CD_MST AS CDMST ON CDMST.CD_CL = 'LOT_OIL_GB' AND AO.LOT_OIL_GB = CDMST.CD_ID
			 WHERE AO.AGGR_DT BETWEEN CAST(DATEADD(YEAR, -1, @P_START_DT) AS NVARCHAR) AND CAST(DATEADD(YEAR, -1, @P_END_DT) AS NVARCHAR)
			   AND (
						ISNULL(@P_LOT_PARTNER_GB, '') = '' 
						OR AO.LOT_PARTNER_GB IN (
							SELECT value FROM STRING_SPLIT(@P_LOT_PARTNER_GB, ',')
						)
					)
			 GROUP BY CDMST.SORT_ORDER, CDMST.CD_ID, CDMST.CD_NM
		)
		SELECT CURR.SORT_ORDER
			 , CURR.LOT_OIL_GB
			 , CURR.LOT_OIL_GB_NM
			 , ISNULL(CURR.AGGR_SUM1, 0) AS CUR_AGGR_SUM
			 , ISNULL(PREV.AGGR_SUM2, 0) AS PRE_AGGR_SUM
		  FROM CURR_DATA AS CURR
		  LEFT JOIN PREV_DATA AS PREV ON CURR.LOT_OIL_GB = PREV.LOT_OIL_GB
		  ORDER BY CURR.SORT_ORDER;



	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH

END

GO

