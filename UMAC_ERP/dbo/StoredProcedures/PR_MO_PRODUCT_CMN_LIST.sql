
/*
-- 생성자 :	이동호
-- 등록일 :	2024.04.22
-- 설 명  : 
			앱(API) 상품리스트 및 상세 출력 
			@P_KEYWORD 값이 있을경우 상품조회 출력
			@P_SCAN_CODE 값이 있을경우 상품상세
-- 수정자 : 최수민
-- 수정일 : 2024.08.26 제품명 -> 단축제품명 변경
			2024.10.04 LOT 수 중복 제거
-- 실행문 : 
EXEC PR_MO_PRODUCT_CMN_LIST '','8801052050683', 'DETAIL'
*/
CREATE PROCEDURE [dbo].[PR_MO_PRODUCT_CMN_LIST]
( 
	@P_KEYWORD			NVARCHAR(100) = '',		-- 검색(명칭) 관리코드, 상품코드, 상품명
	@P_SCAN_CODE		NVARCHAR(14) = '',		-- 상품코드(상세정보 출력)
	@P_MODE				NVARCHAR(10) = ''		-- LIST : 검색, DETAIL : 상세
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 

		IF @P_MODE = 'LIST'
		BEGIN 
			SELECT ITM_CODE, SCAN_CODE, ITM_NAME 
				FROM CD_PRODUCT_CMN 
					WHERE ITM_CODE = @P_KEYWORD OR SCAN_CODE = @P_KEYWORD OR ITM_NAME LIKE '%'+@P_KEYWORD+'%'
		END
		ELSE IF @P_MODE = 'DETAIL'
		BEGIN

			SELECT 
				CMN.ITM_CODE,											--관리코드
				CMN.SCAN_CODE,											--상품코드
				CMN.ITM_NAME,											--제품명
				CMN.ITM_NAME_DETAIL,									--제품명(상세)
				CMN.VEN_CODE,											--거래처코드
				PMST.VEN_NAME,											--거래처명
				CD1.CD_NM AS GRE_GB_NM,									--거래구분								
				LMST.LRG_NAME + '>' + MMST.MID_NAME AS CATEGORY_NM,		--카테고리
				CMN.IPSU_QTY,											--입수량
				CMN.PLT_BOX_QTY,										--PLT박스환산량
				CMN.UNIT_CAPACITY,										--표시용량
				CD3.CD_NM AS UNIT_NM,									--표시용량 단위
				ISNULL(CMN.UNIT_WEIGHT, 0) AS UNIT_WEIGHT,				--상품무게(g)
				CMN.EXPIRY_CNT,											--소비기한일자								
				(CASE WHEN CMN.ITM_FORM = '3' THEN 0 ELSE ISNULL(ST.CUR_INV_QTY, 0) END) AS CUR_INV_QTY,				--현재고수량
				ISNULL(LOT.LOT_NO_CNT, 0) AS LOT_NO_CNT,																--LOT수
				BOX.BOX_CODE,																							--BOX 관리코드
				(CASE WHEN BOX.BOX_CODE IS NOT NULL THEN CMN.ITM_NAME_DETAIL ELSE '' END) AS BOX_NAME,					--BOX 상품명
				CMN.WEIGHT_GB,																									--상품 수/중량 구분 코드
				(CASE WHEN CMN.WEIGHT_GB = 'QTY' THEN 'EA' WHEN CMN.WEIGHT_GB = 'WT' THEN 'KG' ELSE '' END) AS  WEIGHT_GB_NM	--상품 수/중량 구분 단위
			FROM CD_PRODUCT_CMN AS CMN 				
				INNER JOIN CD_MID_MST AS MMST ON MMST.MID_CODE = CMN.MID_CODE
				INNER JOIN CD_LRG_MST AS LMST ON LMST.LRG_CODE = MMST.LRG_CODE
				LEFT OUTER JOIN (
					SELECT LM.SCAN_CODE, COUNT(LM.SCAN_CODE) AS LOT_NO_CNT 
					  FROM VIEW_CD_LOT_MST AS LM 
					 INNER JOIN IV_LOT_STAT AS LS 
						ON LM.SCAN_CODE = LS.SCAN_CODE AND LM.LOT_NO = LS.LOT_NO
					 WHERE LS.CUR_INV_QTY > 0
					 GROUP BY LM.SCAN_CODE
				) AS LOT ON CMN.SCAN_CODE = LOT.SCAN_CODE
				LEFT OUTER JOIN CD_PARTNER_MST AS PMST ON PMST.VEN_CODE = CMN.VEN_CODE
				LEFT OUTER JOIN TBL_COMM_CD_MST AS CD1 ON CD1.CD_CL = 'GRE_GB' AND CD1.CD_ID = CMN.GRE_GB
				LEFT OUTER JOIN TBL_COMM_CD_MST AS CD2 ON CD2.CD_CL = 'ITM_GB' AND CD2.CD_ID = CMN.ITM_GB
				LEFT OUTER JOIN TBL_COMM_CD_MST AS CD3 ON CD3.CD_CL = 'UNIT' AND CD3.CD_ID = CMN.UNIT
				LEFT OUTER JOIN IV_PRODUCT_STAT AS ST ON ST.SCAN_CODE = CMN.SCAN_CODE
				LEFT OUTER JOIN CD_BOX_MST AS BOX ON CMN.ITM_CODE = BOX.ITM_CODE
				WHERE CMN.SCAN_CODE = @P_SCAN_CODE OR CMN.ITM_CODE = @P_SCAN_CODE
		END
					
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

