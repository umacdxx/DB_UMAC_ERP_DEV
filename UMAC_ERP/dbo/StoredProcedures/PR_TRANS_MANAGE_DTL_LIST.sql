/*
-- 생성자 :	강세미
-- 등록일 :	2023.03.18
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 배차관리 DTL 리스트 조회
-- 실행문 : 
EXEC PR_TRANS_MANAGE_DTL_LIST '1240523001'
*/
CREATE PROCEDURE [dbo].[PR_TRANS_MANAGE_DTL_LIST]
( 
	@P_ORD_NO	NVARCHAR(11) = ''			  -- 주문번호
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	DECLARE @IO_GB INT = 0;		-- 입출고구분

	SET @IO_GB = CASE WHEN LEFT(@P_ORD_NO,1) = 1 THEN 1 ELSE 2 END

	BEGIN TRY
		IF @IO_GB = 1 -- 입고
			BEGIN
				SELECT ROW_NUMBER() OVER(ORDER BY SEQ DESC) AS ROW_NUM,
						 A.SCAN_CODE,
						 CAST(A.PUR_QTY AS DECIMAL(15,2)) AS IO_QTY,
						 CASE WHEN B.PLT_BOX_QTY IS NOT NULL THEN CAST(ISNULL(CEILING(A.ORD_QTY / B.PLT_BOX_QTY), 0) AS INT) ELSE 0 END AS PLT_QTY,
						 A.ORD_QTY,
		   				 B.ITM_NAME,
						 B.ITM_NAME_DETAIL,
						 B.UNIT_CAPACITY,						 
						 B.WEIGHT_GB,
						 UCOM.CD_NM AS UNIT_NM
				  FROM PO_PURCHASE_DTL AS A
						 LEFT OUTER JOIN CD_PRODUCT_CMN AS B
							ON B.SCAN_CODE = A.SCAN_CODE
						 LEFT OUTER JOIN TBL_COMM_CD_MST AS UCOM
							ON UCOM.CD_CL = 'UNIT' AND B.UNIT = UCOM.CD_ID
				 WHERE A.ORD_NO = @P_ORD_NO
			END
		ELSE 
			BEGIN
				SELECT ROW_NUMBER() OVER(ORDER BY SEQ DESC) AS ROW_NUM,
						 A.SCAN_CODE,
						 CAST(A.PICKING_QTY AS DECIMAL(15,2)) AS IO_QTY,
						 CASE  WHEN A.SEQ = 0 THEN 0
							   WHEN B.PLT_BOX_QTY IS NOT NULL THEN CAST(ISNULL(CEILING(A.ORD_QTY / B.PLT_BOX_QTY), 0) AS INT) ELSE 0 END AS PLT_QTY,
						 A.ORD_QTY,
		   				 B.ITM_NAME,
						 B.ITM_NAME_DETAIL,
						 B.UNIT_CAPACITY,						 
						 B.WEIGHT_GB,
						 UCOM.CD_NM AS UNIT_NM
				  --FROM PO_ORDER_DTL AS A
				  -- 2024.07.26 윤현빈 추가, 샘플+
					FROM VIEW_ORDER_DTL_SAMPLE_SUM AS A
					LEFT OUTER JOIN CD_PRODUCT_CMN AS B ON B.SCAN_CODE = A.SCAN_CODE
					LEFT OUTER JOIN TBL_COMM_CD_MST AS UCOM ON UCOM.CD_CL = 'UNIT' AND B.UNIT = UCOM.CD_ID
				 WHERE A.ORD_NO = @P_ORD_NO
			END
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

