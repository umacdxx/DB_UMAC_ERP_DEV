/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.05.13
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 시험성적서 관리 상품리스트 조회
-- 실행문 : EXEC PR_TEST_REPORT_DETAIL_PROD_LIST  '','','','','',''
*/
CREATE PROCEDURE [dbo].[PR_TEST_REPORT_DETAIL_PROD_LIST]
	@P_SCAN_CODE		NVARCHAR(14),	-- 상품코드
	@P_FROM_PROD_DT		NVARCHAR(10),	-- from 생산일자
	@P_TO_PROD_DT		NVARCHAR(10),	-- to 생산일자
	@P_ALL_SEARCH		VARCHAR(2),		-- 전체조회 여부
	@P_LOT_NO			NVARCHAR(30),	-- LOT 번호
	@P_LOT_OIL_GB		NVARCHAR(2),	-- 유종
	@P_LOT_PARTNER_GB	NVARCHAR(3)		-- 거래처 구분

AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 
		WITH W_LOT_SCAN_CODE AS (
			SELECT DISTINCT A.SCAN_CODE
				FROM CD_LOT_MST AS A
				INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
				LEFT OUTER JOIN TBL_COMM_CD_MST AS C ON B.LOT_OIL_GB = C.CD_ID AND C.CD_CL = 'LOT_OIL_GB'
				LEFT OUTER JOIN TBL_COMM_CD_MST AS D ON B.LOT_PARTNER_GB = D.CD_ID AND D.CD_CL = 'LOT_PARTNER_GB'
			   WHERE 1=(CASE WHEN @P_ALL_SEARCH = 'Y' THEN 1 WHEN @P_ALL_SEARCH = 'N' AND A.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT THEN 1 ELSE 2 END)
			     AND 1=(CASE WHEN @P_LOT_NO = '' THEN 1 WHEN @P_LOT_NO != '' AND A.LOT_NO LIKE '%'+@P_LOT_NO+'%' THEN 1 ELSE 2 END)
			     AND 1=(CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE != '' AND A.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 2 END)
			     AND 1=(CASE WHEN @P_LOT_OIL_GB = '' THEN 1 WHEN @P_LOT_OIL_GB != '' AND B.LOT_OIL_GB	= @P_LOT_OIL_GB THEN 1 ELSE 2 END)
			     AND 1=(CASE WHEN @P_LOT_PARTNER_GB = '' THEN 1 WHEN @P_LOT_PARTNER_GB != '' AND B.LOT_PARTNER_GB = @P_LOT_PARTNER_GB	THEN 1 ELSE 2 END)
			     
		), W_TEST_REPORT AS (
			SELECT A.SCAN_CODE
			     , COUNT(A.LOT_NO) AS LOT_CNT
				FROM CD_LOT_MST AS A
				INNER JOIN W_LOT_SCAN_CODE AS B ON A.SCAN_CODE = B.SCAN_CODE
			   WHERE 1=(CASE WHEN @P_ALL_SEARCH = 'Y' THEN 1 WHEN @P_ALL_SEARCH = 'N' AND A.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT THEN 1 ELSE 2 END)
			   GROUP BY A.SCAN_CODE
		), W_TEST_REPORT_CNT AS (
			SELECT A.SCAN_CODE
			     , COUNT(A.LOT_NO) AS TEST_REPORT_CNT
			     , MAX(A.UDATE) AS UDATE
				FROM PD_TEST_REPORT AS A
				INNER JOIN W_LOT_SCAN_CODE AS B ON A.SCAN_CODE = B.SCAN_CODE
			   WHERE 1=(CASE WHEN @P_ALL_SEARCH = 'Y' THEN 1 WHEN @P_ALL_SEARCH = 'N' AND A.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT THEN 1 ELSE 2 END)
			   GROUP BY A.SCAN_CODE
		)
		SELECT ROW_NUMBER() OVER(ORDER BY B.ITM_CODE) AS ROW_NUM
		     , A.SCAN_CODE 
		     , A.LOT_CNT
		     , ISNULL(C.TEST_REPORT_CNT, 0) AS TEST_REPORT_CNT
		     , CONVERT(NVARCHAR(8), C.UDATE, 112) AS UDATE
			 , B.ITM_CODE
			 , B.ITM_NAME
		     , D.CD_NM AS LOT_OIL_GB
		     , E.CD_NM AS LOT_PARTNER_GB
		     , CASE WHEN @P_ALL_SEARCH = 'Y' THEN '19000101' ELSE @P_FROM_PROD_DT END AS P_FROM_PROD_DT
		     , CASE WHEN @P_ALL_SEARCH = 'Y' THEN '99991231' ELSE @P_TO_PROD_DT END AS P_TO_PROD_DT
			 , B.ITM_FORM
			FROM W_TEST_REPORT AS A
			INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
			LEFT OUTER JOIN W_TEST_REPORT_CNT AS C ON A.SCAN_CODE = C.SCAN_CODE
			LEFT OUTER JOIN TBL_COMM_CD_MST AS D ON B.LOT_OIL_GB = D.CD_ID AND D.CD_CL = 'LOT_OIL_GB'
			LEFT OUTER JOIN TBL_COMM_CD_MST AS E ON B.LOT_PARTNER_GB = E.CD_ID AND E.CD_CL = 'LOT_PARTNER_GB'
			;
		
	END TRY
	BEGIN CATCH	
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH

END

GO

