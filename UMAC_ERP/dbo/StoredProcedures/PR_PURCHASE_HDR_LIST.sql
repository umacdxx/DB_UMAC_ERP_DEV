/*
-- 생성자 :	강세미
-- 등록일 :	2024.03.04
-- 수정자 : 2024.07.31 강세미 - 입고예정일(DELIVERY_EXP_DT) 추가
-- 설 명  : 발주등록 HDR 리스트 조회
-- 실행문 : 
EXEC PR_PURCHASE_HDR_LIST '','20240724','20240731','',''
*/
CREATE PROCEDURE [dbo].[PR_PURCHASE_HDR_LIST]
( 
	@P_VEN_CODE	NVARCHAR(7) = '',   -- 거래처코드
	@P_START_DT	NVARCHAR(8) = '',   -- 조회시작일자
	@P_END_DT	NVARCHAR(8) = '',   -- 조회종료일자
	@P_PUR_STAT	NVARCHAR(2) = '',   -- 발주상태
	@P_ORD_NO	NVARCHAR(11) = ''   -- 발주번호
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
 
	BEGIN TRY 
	  SELECT ROW_NUMBER() OVER(ORDER BY ORD_NO DESC) AS ROW_NUM,
				A.ORD_NO,
				A.ORD_DT,
				A.DELIVERY_IN_DT,
				A.ORD_AMT,
				A.VEN_CODE,
				A.DELIVERY_CODE,
				A.R_POST_NO,
				A.R_ADDR,
				A.R_ADDR_DTL,
				A.PUR_STAT,
				A.PUR_CFM,
				A.PUR_CFM_DT,
				A.TRANS_YN,
				A.REMARKS,
				A.DELIVERY_EXP_DT,
				B.VEN_NAME,
				C.DELIVERY_GB,
				C.DELIVERY_NAME,
				D.CD_NM AS PUR_STAT_NM
		FROM PO_PURCHASE_HDR AS A
		INNER JOIN CD_PARTNER_MST AS B
					ON B.VEN_CODE = A.VEN_CODE
		LEFT OUTER JOIN CD_PARTNER_DELIVERY AS C
					ON C.VEN_CODE = A.VEN_CODE AND C.DELIVERY_CODE = A.DELIVERY_CODE
		INNER JOIN TBL_COMM_CD_MST AS D
					ON D.CD_CL = 'PUR_STAT' AND A.PUR_STAT = D.CD_ID
		
	  WHERE (A.ORD_NO = CASE WHEN @P_ORD_NO <> '' THEN @P_ORD_NO ELSE A.ORD_NO END) 
				AND A.VEN_CODE = (CASE WHEN @P_VEN_CODE <> '' THEN @P_VEN_CODE ELSE A.VEN_CODE END) 
				AND A.PUR_STAT = (CASE WHEN @P_PUR_STAT <> '' THEN @P_PUR_STAT ELSE A.PUR_STAT END) 
				AND (CASE WHEN A.ORD_DT BETWEEN @P_START_DT AND @P_END_DT THEN 1 ELSE 0 END) = 1
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

