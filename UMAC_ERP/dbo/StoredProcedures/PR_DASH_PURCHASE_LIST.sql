
/*
-- 생성자 :	최수민
-- 등록일 :	2024.07.19
-- 설 명  : 대시보드 입고 예정 목록 조회
-- 수정자 :	최수민
-- 수정일 :	2024.07.19
-- 설 명  : 
-- 실행문 : EXEC PR_DASH_PURCHASE_LIST '20240705', '20240705'
*/
CREATE PROCEDURE [dbo].[PR_DASH_PURCHASE_LIST]
(
	@P_START_DT		NVARCHAR(8) = '',			-- 조회시작일자
	@P_END_DT		NVARCHAR(8) = '',			-- 조회종료일자
	@P_PUR_STAT	    NVARCHAR(2) = NULL			-- 발주 상태
)
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;


	BEGIN TRY 

		SELECT ROW_NUMBER() OVER(ORDER BY ORD_DT) AS ROW_NUM
			 , HDR.ORD_NO
			 , HDR.ORD_DT
			 , C.VEN_NAME
			 , D.CD_NM AS PUR_STAT_NM
			 , CASE WHEN COUNT(CMN.SCAN_CODE) = 1 THEN MIN(CMN.ITM_NAME)
					ELSE CONCAT(Min(CMN.ITM_NAME), ' 외 ', COUNT(CMN.SCAN_CODE) - 1)
			   END AS ITM_NAME
		  FROM PO_PURCHASE_HDR AS HDR
		 INNER JOIN PO_PURCHASE_DTL AS DTL ON HDR.ORD_NO = DTL.ORD_NO
		 INNER JOIN CD_PRODUCT_CMN AS CMN ON DTL.SCAN_CODE = CMN.SCAN_CODE
		 INNER JOIN CD_PARTNER_MST AS C ON HDR.VEN_CODE = C.VEN_CODE
		  LEFT OUTER JOIN TBL_COMM_CD_MST AS D ON D.CD_CL = 'PUR_STAT' AND HDR.PUR_STAT = D.CD_ID
		 WHERE HDR.DELIVERY_EXP_DT BETWEEN @P_START_DT AND @P_END_DT
		   AND ((@P_PUR_STAT IS NULL AND HDR.PUR_STAT IN ('10','25','33','35','40'))
			OR (@P_PUR_STAT IS NOT NULL AND HDR.PUR_STAT = @P_PUR_STAT))
		 GROUP BY HDR.ORD_NO
				, HDR.ORD_DT
				, C.VEN_NAME
				, D.CD_NM
		 ORDER BY ORD_DT

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

