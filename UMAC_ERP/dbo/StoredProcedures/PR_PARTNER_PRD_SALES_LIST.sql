/*
-- 생성자 :	강세미
-- 등록일 :	2024.06.04
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 거래처 상품상세 매출조회
-- 실행문 : EXEC PR_PARTNER_PRD_SALES_LIST '2240704002'
*/
CREATE PROCEDURE [dbo].[PR_PARTNER_PRD_SALES_LIST]
	@P_ORD_NO			VARCHAR(11)	-- 주문번호
AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 

		SELECT 
			ROW_NUMBER() OVER(ORDER BY B.ITM_NAME DESC) AS ROW_NUM,
			(CASE WHEN A.GUBUN = 'S' THEN CONCAT('(샘플)', A.SCAN_CODE) ELSE A.SCAN_CODE END) AS SCAN_CODE,
			A.SALE_TOTAL_AMT,
			B.UNIT_CAPACITY,
			(CASE WHEN A.GUBUN = 'S' THEN CONCAT('(샘플)', B.ITM_NAME) ELSE B.ITM_NAME END) AS ITM_NAME,
			CAST(ISNULL(A.BEFO_QTY, 0) AS INT) AS BEFO_QTY,   -- 변경 전 수량			
			ROUND((CASE WHEN A.BEFO_AMT = 0 THEN 0 ELSE ISNULL(A.BEFO_AMT / A.BEFO_QTY, 0) END), 1) AS BEFO_AMT,   -- 변경 전 단가
			ISNULL(A.BEFO_AMT, 0) AS BEFO_TOTAL_AMT,   -- 변경 전 합계
			(CASE WHEN B.WEIGHT_GB = 'QTY' THEN CAST(A.SALE_EA AS INT)
				  WHEN B.WEIGHT_GB = 'WT' THEN CAST(A.SALE_KG AS INT) END) AS SALE_QTY,
			ROUND((CASE WHEN A.SALE_TOTAL_AMT = 0 THEN 0
				  WHEN B.WEIGHT_GB = 'QTY' THEN ISNULL(A.SALE_TOTAL_AMT / A.SALE_EA, 0)
				  WHEN B.WEIGHT_GB = 'WT' THEN ISNULL(A.SALE_TOTAL_AMT / A.SALE_KG, 0) END), 1) AS SALE_AMT,
			C.CD_NM AS UNIT_CAPACITY_NM,																		--표시 용량(규격) 단위
			B.WEIGHT_GB
		FROM SL_SALE AS A
		INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
	    INNER JOIN TBL_COMM_CD_MST AS C ON C.CD_CL='UNIT' AND B.UNIT = C.CD_ID
		WHERE A.ORD_NO = @P_ORD_NO;

	
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

