/*
-- 생성자 :	강세미
-- 등록일 :	2024.09.12
-- 수정자 : 2024.10.30 강세미 - 로직변경
			2024.12.03 강세미 - 담당자 조회조건 수정
-- 설 명  : 매출마감확정내역
-- 실행문 : EXEC PR_CLOSING_ORDER_LIST '202409','','',''
*/
CREATE PROCEDURE [dbo].[PR_CLOSING_ORDER_LIST]
	@P_CLOSE_DT			NVARCHAR(6),		-- 마감월
	@P_DEPT_CODE		NVARCHAR(25),		-- 조직코드
	@P_VEN_CODE			NVARCHAR(7),		-- 거래처코드
	@P_MGNT_USER_ID		NVARCHAR(300)		-- 담당자ID (',' 구분)
AS 
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 
		
		WITH ORDER_TBL AS (
			SELECT  LEFT(A.CLOSE_DT, 6) AS CLOSE_MM,
					A.VEN_CODE,
					A.ISSUE_GB,
					A.DOUZONE_FLAG,
					A.CLOSE_EMP_ID,
					A.CLOSE_STAT,
					ISNULL(A.CLOSE_NO, '') AS CLOSE_NO,
					--ROUND((SUM(A.PICKING_TOTAL_AMT) / 1.1), 0) AS PICKING_TOTAL_SPRC,
					--ROUND(SUM(A.PICKING_TOTAL_AMT) - (SUM(A.PICKING_TOTAL_AMT) / 1.1), 0) AS PICKING_TOTAL_SVAT,
					
					( CASE WHEN C.VEN_GB = '4' THEN ROUND(SUM(A.PICKING_TOTAL_AMT), 0) 
						   ELSE ROUND((SUM(A.PICKING_TOTAL_AMT) / 1.1), 0) END ) AS PICKING_TOTAL_SPRC,
					( CASE WHEN C.VEN_GB = '4' THEN 0
						   ELSE ROUND(SUM(A.PICKING_TOTAL_AMT) - (SUM(A.PICKING_TOTAL_AMT) / 1.1), 0) END ) AS PICKING_TOTAL_SVAT,
					SUM(A.PICKING_TOTAL_AMT) AS PICKING_TOTAL_SAMT,
					B.CD_NM AS ISSUE_GB_NM
			FROM PO_ORDER_HDR AS A
			INNER JOIN TBL_COMM_CD_MST AS B ON B.CD_CL = 'ISSUE_GB' AND B.CD_ID = A.ISSUE_GB
			INNER JOIN CD_PARTNER_MST AS C ON A.VEN_CODE = C.VEN_CODE
			WHERE A.CLOSE_DT LIKE @P_CLOSE_DT + '%' 
				AND A.CLOSE_STAT = 'Y'
			GROUP BY A.VEN_CODE, LEFT(A.CLOSE_DT, 6), A.CLOSE_NO, A.ISSUE_GB, A.CLOSE_EMP_ID, A.CLOSE_STAT, A.DOUZONE_FLAG, B.CD_NM, C.VEN_GB
		), RECEIVABLES AS (
			SELECT 
				VEN_CODE,
				SUM(CASE WHEN DEPOSIT_AMT < 0 THEN DEPOSIT_AMT ELSE SALE_TOTAL_AMT - DEPOSIT_AMT END) AS RECEIVABLE_AMT
			FROM PA_ACCT_DEPOSIT_ORD			
			GROUP BY VEN_CODE
		)
		SELECT A.CLOSE_MM,
			   A.VEN_CODE,
			   A.CLOSE_NO,
			   A.ISSUE_GB,
			   A.ISSUE_GB_NM,
			   B.VEN_NAME,
			   C.USER_NM AS MGNT_USER_NM,
			   D.DEPT_NAME,
			   CASE WHEN A.ISSUE_GB = '2' THEN RECEIVABLE_AMT ELSE 0 END AS RECEIVABLE_AMT, --미수금
			   A.PICKING_TOTAL_SPRC,	-- 실마감금액(공급가)
			   A.PICKING_TOTAL_SVAT,	-- 실마감금액(부가세)
			   A.PICKING_TOTAL_SAMT,		--실마감금액(합계)
			   ISNULL(A.CLOSE_STAT, 'N') AS CLOSE_STAT,
			   F.USER_NM AS CLOSE_EMP_NM,
			   ISNULL(A.DOUZONE_FLAG, 'N') AS DOUZONE_FLAG
		  FROM ORDER_TBL AS A
		  INNER JOIN CD_PARTNER_MST AS B
			ON A.VEN_CODE = B.VEN_CODE
		  LEFT OUTER JOIN TBL_USER_MST AS C
			ON B.MGNT_USER_ID = C.[USER_ID]
		  LEFT OUTER JOIN TBL_DEPT_MST AS D
			ON C.DEPT_CODE = D.DEPT_CODE
		  LEFT OUTER JOIN TBL_USER_MST AS F
			ON A.CLOSE_EMP_ID = F.[USER_ID]
		  LEFT OUTER JOIN RECEIVABLES AS G
			ON A.VEN_CODE = G.VEN_CODE
		  WHERE A.CLOSE_MM LIKE @P_CLOSE_DT + '%' 
				AND 1 = (CASE WHEN @P_DEPT_CODE = '' THEN 1 WHEN @P_DEPT_CODE <> '' AND D.DEPT_CODE LIKE @P_DEPT_CODE + '%' THEN 1 ELSE 0 END)
				AND (
						@P_MGNT_USER_ID = ''
						OR
						( B.MGNT_USER_ID IN (SELECT VALUE FROM STRING_SPLIT(@P_MGNT_USER_ID, ','))
							OR ( 'ETC' IN (SELECT VALUE FROM STRING_SPLIT(@P_MGNT_USER_ID, ','))
								 AND (B.MGNT_USER_ID IS NULL OR B.MGNT_USER_ID = ''))
						)
					)
				AND 1 = (CASE WHEN @P_VEN_CODE = '' THEN 1 WHEN @P_VEN_CODE <> '' AND B.VEN_CODE = @P_VEN_CODE THEN 1 ELSE 0 END)
		  ORDER BY A.VEN_CODE

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

