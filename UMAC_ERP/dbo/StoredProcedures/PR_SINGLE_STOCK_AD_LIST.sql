/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.06.05
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 단품재고조정 조회
			2024.11.12 최수민 CH_LOT_NO 컬럼 추가
			2024.11.29 최수민 IEMP_ID 파라미터 추가
-- 실행문 : EXEC PR_SINGLE_STOCK_AD_LIST '','','','',''
*/
CREATE PROCEDURE [dbo].[PR_SINGLE_STOCK_AD_LIST]
( 
	@P_SCAN_CODE	NVARCHAR(14) = '',	-- 상품코드
	@P_FROM_DT		NVARCHAR(8) = '',	-- FROM 조정일자
	@P_TO_DT		NVARCHAR(8) = '',	-- TO 조정일자
	@P_LRG_CODE	 	NVARCHAR(2) = '',	-- 대분류 코드
	@P_MID_CODE	 	NVARCHAR(4) = '',	-- 중분류 코드
	@P_IEMP_ID		NVARCHAR(20) = ''	-- 등록자ID
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
		SELECT ROW_NUMBER() OVER(ORDER BY A.CFM_FLAG, A.INV_DT, A.SCAN_CODE, A.CFM_DT DESC, A.SEQ DESC) AS ROW_NUM
		     , A.SEQ
			 , A.CFM_FLAG AS CFM_FLAG_CD
			 , CASE WHEN A.CFM_FLAG = 'N' THEN '미확정' WHEN A.CFM_FLAG = 'Y' THEN '확정' END CFM_FLAG
			 , A.INV_DT
			 , A.CFM_DT
			 , LRG.LRG_NAME
			 , MID.MID_NAME
			 , A.SCAN_CODE
			 , B.ITM_NAME
			 , B.UNIT_CAPACITY
			 , UCOM.CD_NM AS UNIT_NM
			 , B.WEIGHT_GB			
			 , A.LOT_NO
			 , CHSTOCK.LOT_NO AS CH_LOT_NO
			 , A.REQ_QTY
			 , A.APP_QTY
			 , A.INV_GB
			 , COM_AD.CD_NM AS INV_GB_NM
			 , A.REMARKS
			 , MST_USER.USER_NM AS IEMP_ID
			 , CONVERT(NVARCHAR(8), A.IDATE, 112) AS IDATE
			FROM IV_STOCK_ADJUST AS A
			INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
			INNER JOIN CD_MID_MST AS MID ON B.MID_CODE = MID.MID_CODE 
			INNER JOIN CD_LRG_MST AS LRG ON MID.LRG_CODE = LRG.LRG_CODE 
			INNER JOIN TBL_COMM_CD_MST AS COM_AD ON A.INV_GB = COM_AD.CD_ID AND COM_AD.CD_CL = 'INV_ADJ_GB'
			LEFT OUTER JOIN TBL_USER_MST AS MST_USER ON A.IEMP_ID = MST_USER.USER_ID
			LEFT OUTER JOIN TBL_COMM_CD_MST AS UCOM ON UCOM.CD_CL = 'UNIT' AND B.UNIT = UCOM.CD_ID
			LEFT OUTER JOIN IV_STOCK_ADJUST AS CHSTOCK ON A.SEQ = CHSTOCK.CH_SEQ
		   WHERE A.INV_DT BETWEEN @P_FROM_DT AND @P_TO_DT
		     AND A.CFM_FLAG != 'D' -- 취소된 것 제외
			 AND ISNULL(A.APP_QTY, 1) != 0 -- ISNULL 처리 이유: 확정되지 않은 것 조회하기 위함
		     AND 1=(CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE != '' AND A.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 2 END)
		     AND 1=(CASE WHEN @P_LRG_CODE = '' THEN 1 WHEN @P_LRG_CODE != '' AND LRG.LRG_CODE = @P_LRG_CODE THEN 1 ELSE 2 END)
		     AND 1=(CASE WHEN @P_MID_CODE = '' THEN 1 WHEN @P_MID_CODE != '' AND MID.MID_CODE = @P_MID_CODE THEN 1 ELSE 2 END)
		     AND 1=(CASE WHEN @P_IEMP_ID = '' THEN 1 WHEN @P_IEMP_ID != '' AND A.IEMP_ID = @P_IEMP_ID THEN 1 ELSE 2 END)
			 AND B.ITM_FORM != '3'

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

