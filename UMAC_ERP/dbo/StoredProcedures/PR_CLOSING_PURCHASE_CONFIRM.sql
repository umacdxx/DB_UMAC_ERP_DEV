
/*

-- 생성자 :	강세미
-- 등록일 :	2024.09.27
-- 수정일 : 2024.10.31 강세미 - 로직 변경
-- 설 명  : 월 매입마감 확정
-- 실행문 : 

EXEC PR_CLOSING_PURCHASE_CONFIRM '[{"ORD_NO":"2241010007", "CLOSE_DT": "20241011", "CONFIRM_TYPE": "CONFIRM"},
{"ORD_NO":"2241010007", "CLOSE_DT": "20241011", "CONFIRM_TYPE": "CONFIRM"}]','admin'
EXEC PR_CLOSING_PURCHASE_CONFIRM '[{"ORD_NO":"2241010007", "CLOSE_DT": "20241011", "CONFIRM_TYPE": "CANCEL"}]','admin'

*/
CREATE PROCEDURE [dbo].[PR_CLOSING_PURCHASE_CONFIRM]
( 
	@P_JSONDT			VARCHAR(MAX) = '',
	@P_EMP_ID			NVARCHAR(20)	-- 아이디
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @RETURN_CODE	 INT = 0									-- 리턴코드(저장완료)
	DECLARE @RETURN_MESSAGE  NVARCHAR(MAX) = DBO.GET_ERR_MSG('0')		-- 리턴메시지
	DECLARE @NEW_CLOSE_DT	 NVARCHAR(8)								-- 마감일자
	
	BEGIN TRAN
	BEGIN TRY 
		DECLARE @TMP_ITEM TABLE (
				ORD_NO NVARCHAR(11),
				CONFIRM_TYPE NVARCHAR(10),
				CLOSE_DT NVARCHAR(8),
				ISSUE_GB NVARCHAR(1)
		)
		
		INSERT INTO @TMP_ITEM
		SELECT ORD_NO, CONFIRM_TYPE, CLOSE_DT, ISSUE_GB
			FROM 
				OPENJSON ( @P_JSONDT )   
					WITH (    
						ORD_NO NVARCHAR(11) '$.ORD_NO',
						CONFIRM_TYPE NVARCHAR(10) '$.CONFIRM_TYPE',
						CLOSE_DT NVARCHAR(8) '$.CLOSE_DT',
						ISSUE_GB NVARCHAR(1) '$.ISSUE_GB'
					)
				
		DECLARE CURSOR_REMARKS CURSOR FOR

			SELECT A.ORD_NO, A.CONFIRM_TYPE, A.CLOSE_DT, A.ISSUE_GB
				FROM @TMP_ITEM A
			
		OPEN CURSOR_REMARKS

		DECLARE @P_ORD_NO NVARCHAR(11),
				@P_CONFIRM_TYPE NVARCHAR(10),
				@P_CLOSE_DT NVARCHAR(8),
				@P_ISSUE_GB NVARCHAR(1)

		FETCH NEXT FROM CURSOR_REMARKS INTO @P_ORD_NO, @P_CONFIRM_TYPE, @P_CLOSE_DT, @P_ISSUE_GB
			
			WHILE(@@FETCH_STATUS=0)
			BEGIN
				-- ************************************************************
				-- 확정여부 업데이트
				-- ************************************************************

				IF @P_CONFIRM_TYPE = 'CONFIRM' --확정
				BEGIN

					-- 마감일변경 여부가 'N'인 경우 마감일자 해당 월의 말일로 수정
					SET @NEW_CLOSE_DT = CONVERT(VARCHAR(8),EOMONTH(CONVERT(DATE, @P_CLOSE_DT)), 112)

					UPDATE PO_PURCHASE_HDR 
					   SET CLOSE_DT = CASE WHEN CLOSE_DT_CHG_YN = 'N' THEN @NEW_CLOSE_DT ELSE CLOSE_DT END,
					       CLOSE_STAT = 'Y',
						   CLOSE_EMP_ID = @P_EMP_ID,
						   UDATE = GETDATE(),
						   UEMP_ID = @P_EMP_ID
					 WHERE ORD_NO = @P_ORD_NO

				END
				ELSE IF @P_CONFIRM_TYPE = 'CANCEL' --확정취소
				BEGIN

					UPDATE PO_PURCHASE_HDR 
					   SET CLOSE_STAT = 'N',
						   CLOSE_DT = DELIVERY_IN_DT,
						   CLOSE_DT_CHG_YN = 'N',
						   CLOSE_EMP_ID = NULL,
						   UDATE = GETDATE(),
						   UEMP_ID = @P_EMP_ID
					 WHERE ORD_NO = @P_ORD_NO

				END

				FETCH NEXT FROM CURSOR_REMARKS INTO @P_ORD_NO, @P_CONFIRM_TYPE, @P_CLOSE_DT, @P_ISSUE_GB

			END

		CLOSE CURSOR_REMARKS
		DEALLOCATE CURSOR_REMARKS
		COMMIT;
	END TRY
	
	BEGIN CATCH	
		
		IF @@TRANCOUNT > 0
		BEGIN 
			ROLLBACK TRAN
			SET @RETURN_CODE = -1
			SET @RETURN_MESSAGE = ERROR_MESSAGE()

			--에러 로그 테이블 저장
			INSERT INTO TBL_ERROR_LOG 
			SELECT ERROR_PROCEDURE()		-- 프로시저명
				, ERROR_MESSAGE()			-- 에러메시지
				, ERROR_LINE()				-- 에러라인
				, GETDATE()	
		END 

	END CATCH
	SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE 
END

GO

