/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.08.13
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : SET 작업 조회, SET 생산계획
-- 실행문 : EXEC PR_SET_PLAN_REGISTER_LIST '','',''
*/
CREATE PROCEDURE [dbo].[PR_SET_PLAN_REGISTER_LIST]
( 
	@P_SET_PLAN_ID			NVARCHAR(11) = '',		-- 작업코드
	@P_SET_PROD_SDATE		NVARCHAR(8) = '',		-- 생산시작일자
	@P_SET_PROD_EDATE		NVARCHAR(8) = ''		-- 생산종료일자
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
	
		WITH W_RESULT_PROD AS (
			SELECT SET_PLAN_ID
				 , SCAN_CODE
				 , SUM(PROD_QTY) AS PROD_QTY
				FROM PD_SET_RESULT_PROD
			   WHERE PROD_DT BETWEEN @P_SET_PROD_SDATE AND @P_SET_PROD_EDATE
			     AND SET_PLAN_ID = @P_SET_PLAN_ID
			   GROUP BY SET_PLAN_ID, SCAN_CODE
		)

		SELECT A.SET_PLAN_ID
		     , A.SCAN_CODE
			 , MST_PRD.ITM_CODE
			 , MST_PRD.ITM_NAME_DETAIL AS ITM_NAME
			 , CASE WHEN MST_PRD.IPSU_QTY IS NOT NULL THEN A.PLAN_QTY / MST_PRD.IPSU_QTY ELSE NULL END AS PLAN_BOX_QTY
			 , A.PLAN_QTY
			 , ISNULL(B.PROD_QTY, 0) AS TOT_PROD_QTY
			 , MST_PRD.LOT_OIL_GB
			 , MST_PRD.LOT_PARTNER_GB
			 , MST_PRD.PLT_BOX_QTY
			 , MST_PRD.IPSU_QTY
			 --, ISNULL(CONVERT(NVARCHAR(10), FORMAT(CAST(A.SET_EXPIRY_DATE AS DATE), 'yyyy-MM-dd'), 120), CONVERT(NVARCHAR(10), DATEADD(DAY, -1, DATEADD(YEAR, 2, GETDATE())), 120)) AS SET_EXPIRY_DATE  
			 , CONVERT(NVARCHAR(10), FORMAT(CAST(A.SET_EXPIRY_DATE AS DATE), 'yyyy-MM-dd'), 120) AS SET_EXPIRY_DATE  
			FROM PD_SET_PLAN_PROD AS A
			INNER JOIN CD_PRODUCT_CMN AS MST_PRD ON A.SCAN_CODE = MST_PRD.SCAN_CODE
			LEFT OUTER JOIN W_RESULT_PROD AS B ON A.SET_PLAN_ID = B.SET_PLAN_ID AND A.SCAN_CODE = B.SCAN_CODE
		   WHERE A.SET_PLAN_ID = @P_SET_PLAN_ID
		   ORDER BY A.SCAN_CODE

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

