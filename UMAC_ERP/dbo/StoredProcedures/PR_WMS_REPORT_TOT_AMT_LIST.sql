/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.03.11
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 입출고 현황 금액 합 리스트
-- 실행문 : EXEC PR_WMS_REPORT_TOT_AMT_LIST '','20240101','20240327','20240101','20240327','','','','','';
*/
CREATE PROCEDURE [dbo].[PR_WMS_REPORT_TOT_AMT_LIST]
( 
	@P_IO_GB				INT				= 0,	-- 입출고구분 (입고:1, 출고:2)
	@P_FROM_ORD_DT			NVARCHAR(10)	= '',	-- 주문(발주) 일자
	@P_TO_ORD_DT			NVARCHAR(10)	= '',	-- 주문(발주) 일자
	@P_FROM_DELIVERY_REQ_DT	NVARCHAR(10)	= '',	-- 출고요청 일자
	@P_TO_DELIVERY_REQ_DT	NVARCHAR(10)	= '',	-- 출고요청 일자
	@P_ORD_STAT				NVARCHAR(30)	= '',	-- 출고상태
	@P_PUR_STAT				NVARCHAR(30)	= '',	-- 입고상태
	@P_VEN_CODE				NVARCHAR(50)	= '',	-- 거래처명(코드)
	@P_ORD_NO				NVARCHAR(11)	= '',	-- 주문(발주)번호
	@P_SCAN_CODE			NVARCHAR(14)	= ''	-- 상품명(코드)
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 
		WITH LIST AS (

			SELECT 2 AS IO_GB
				, B.IO_AMT
			FROM PO_ORDER_HDR AS A INNER JOIN (
				SELECT A.ORD_NO, SUM(ISNULL(B.PICKING_SAMT, 0)) AS IO_AMT   
					FROM PO_ORDER_HDR AS A	
						INNER JOIN PO_ORDER_DTL AS B ON A.ORD_NO = B.ORD_NO		
						WHERE 1=(CASE WHEN @P_IO_GB = 1 THEN 2 ELSE 1 END)
								AND 1=(CASE WHEN @P_ORD_NO = '' THEN 1 WHEN @P_ORD_NO <> '' AND A.ORD_NO LIKE '%'+@P_ORD_NO+'%' THEN 1 ELSE 0 END ) 
								AND A.ORD_DT BETWEEN  @P_FROM_ORD_DT AND @P_TO_ORD_DT
								AND 1 = (CASE WHEN @P_VEN_CODE = '' THEN 1 WHEN  @P_VEN_CODE <> '' AND @P_VEN_CODE = A.VEN_CODE THEN 1 ELSE 0 END )
								AND A.DELIVERY_REQ_DT BETWEEN @P_FROM_DELIVERY_REQ_DT AND @P_TO_DELIVERY_REQ_DT						
								AND 1 = (CASE WHEN @P_ORD_STAT = '' THEN 1 WHEN  @P_ORD_STAT <> '' AND @P_ORD_STAT = A.ORD_STAT THEN 1 ELSE 0 END ) 							
								AND 1 = (CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE <> '' AND B.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 0 END) 
							GROUP BY A.ORD_NO
			)  AS B ON A.ORD_NO = B.ORD_NO

			UNION ALL 

			SELECT 1 AS IO_GB
				, B.IO_AMT
			FROM PO_PURCHASE_HDR AS A INNER JOIN (
					SELECT A.ORD_NO, SUM(ISNULL(B.PUR_WAMT,0)) AS IO_AMT   
						FROM PO_PURCHASE_HDR AS A	
							INNER JOIN PO_PURCHASE_DTL AS B ON A.ORD_NO = B.ORD_NO
							WHERE 1 = (CASE WHEN @P_IO_GB = 2 THEN 2 ELSE 1 END)									
									AND 1 = (CASE WHEN @P_ORD_NO = '' THEN 1 WHEN @P_ORD_NO <> '' AND A.ORD_NO LIKE '%'+@P_ORD_NO+'%' THEN 1 ELSE 0 END )
									AND A.ORD_DT BETWEEN  @P_FROM_ORD_DT AND @P_TO_ORD_DT								
									AND 1 = (CASE WHEN @P_VEN_CODE = '' THEN 1 WHEN  @P_VEN_CODE <> '' AND @P_VEN_CODE = A.VEN_CODE THEN 1 ELSE 0 END ) 
									AND 1 = (CASE WHEN @P_PUR_STAT = '' THEN 1 WHEN  @P_PUR_STAT <> '' AND @P_PUR_STAT = A.PUR_STAT THEN 1 ELSE 0 END ) 			
									AND 1 = (CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE <> '' AND B.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 0 END) 
					GROUP BY A.ORD_NO
			)  AS B ON A.ORD_NO = B.ORD_NO
		), W_MAIN AS (
					SELECT W.IO_GB
						 , SUM(ISNULL(W.IO_AMT, 0)) AS TOT_AMT
						FROM LIST AS W
					GROUP BY W.IO_GB
		)
			SELECT W.IO_GB
				 , W.TOT_AMT
				FROM W_MAIN AS W
			UNION ALL
			SELECT 1 AS IO_GB
			     , 0 AS TOT_AMT
			WHERE NOT EXISTS (SELECT 1 FROM W_MAIN WHERE IO_GB = 1)
			UNION ALL
			SELECT 2 AS IO_GB
			     , 0 AS TOT_AMT
			WHERE NOT EXISTS (SELECT 1 FROM W_MAIN WHERE IO_GB = 2)
			ORDER BY IO_GB
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

