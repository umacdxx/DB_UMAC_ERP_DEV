/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.05.17
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 상품별 LOT 조회
-- 실행문 : EXEC PR_LOT_LIST '','','','20240901','20241003','',''
*/
CREATE PROCEDURE [dbo].[PR_LOT_LIST]
( 
	@P_VEN_CODE			NVARCHAR(7) = '',	-- 거래처코드
	@P_SCAN_CODE		NVARCHAR(14) = '',	-- 상품코드
	@P_LOT_NO	 		NVARCHAR(30) = '',	-- LOT번호
	@P_IS_PROD	 		NVARCHAR(30) = '',	-- 생산 체크박스
	@P_IS_EXPI	 		NVARCHAR(30) = '',	-- 소비기한 체크박스
	@P_FROM_PROD_DT	 	NVARCHAR(8) = '',	-- FROM 생산일자
	@P_TO_PROD_DT	 	NVARCHAR(8) = '', 	-- TO 생산일자
	@P_FROM_EXPIRY_DT	NVARCHAR(8) = '',	-- FROM 소비기한
	@P_TO_EXPIRY_DT	 	NVARCHAR(8) = ''	-- TO 소비기한
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
	
		WITH W_DATA AS (
			SELECT A.SCAN_CODE
			     --, SUBSTRING(A.LOT_NO, 0, CHARINDEX('-', A.LOT_NO)) AS EXPIRY_DT
			     , A.CUR_INV_QTY
			     , B.PROD_DT
			     , B.LOT_NO
				 , B.EXPIRATION_DT AS EXPIRY_DT
				FROM IV_LOT_STAT AS A
				INNER JOIN CD_LOT_MST AS B ON A.SCAN_CODE = B.SCAN_CODE AND A.LOT_NO = B.LOT_NO
			   WHERE 1=(CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE != '' AND A.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 2 END)
			     AND 1=(CASE WHEN @P_IS_PROD = 'N' THEN 1 WHEN B.PROD_DT BETWEEN @P_FROM_PROD_DT AND @P_TO_PROD_DT THEN 1 ELSE 2 END) 
			     AND 1=(CASE WHEN @P_LOT_NO = '' THEN 1 WHEN @P_LOT_NO != '' AND B.LOT_NO LIKE '%'+@P_LOT_NO+'%' THEN 1 ELSE 2 END)
				 AND A.CUR_INV_QTY != 0
			     
		)
		SELECT A.SCAN_CODE
		     , B.ITM_NAME
		     , A.LOT_NO
		     , A.PROD_DT
		     , A.EXPIRY_DT 
		     , CONCAT(FORMAT(A.CUR_INV_QTY, 'N0'), ' EA(', FORMAT(ROUND(CAST(A.CUR_INV_QTY * ISNULL(B.NET_WEIGHT, 0) AS FLOAT) / 1000, 1), 'N2'), ' KG)') AS CUR_INV_QTY
			FROM W_DATA AS A
			INNER JOIN CD_PRODUCT_CMN AS B ON A.SCAN_CODE = B.SCAN_CODE
		   WHERE 1=(CASE WHEN @P_IS_EXPI = 'N' THEN 1 WHEN A.EXPIRY_DT BETWEEN @P_FROM_EXPIRY_DT AND @P_TO_EXPIRY_DT THEN 1 ELSE 2 END)
   		     AND 1=(CASE WHEN @P_VEN_CODE = '' THEN 1
                 WHEN @P_VEN_CODE != '' AND B.VEN_CODE = @P_VEN_CODE THEN 1
                 WHEN @P_VEN_CODE != '' AND B.VEN_CODE_2 = @P_VEN_CODE THEN 1
                 WHEN @P_VEN_CODE != '' AND B.VEN_CODE_3 = @P_VEN_CODE THEN 1
                 WHEN @P_VEN_CODE != '' AND B.VEN_CODE_4 = @P_VEN_CODE THEN 1
                 WHEN @P_VEN_CODE != '' AND B.VEN_CODE_5 = @P_VEN_CODE THEN 1 ELSE 2 END
		     )
			 AND B.ITM_FORM != '3'
		   ORDER BY A.SCAN_CODE, A.LOT_NO DESC

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

