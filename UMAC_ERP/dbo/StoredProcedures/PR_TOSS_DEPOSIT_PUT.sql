

/*

-- 생성자 :	이동호
-- 등록일 :	2024.05.23
-- 수정자 : -
-- 수정일 : -
-- 설 명  : 
-- 실행문 : 토스 정산 data 일자별 저장


*/
CREATE PROCEDURE [dbo].[PR_TOSS_DEPOSIT_PUT]
( 	
	@P_DEPOSIT_DT		NVARCHAR(8),		-- 입금일자
	@P_JSONDT			NVARCHAR(MAX)		-- 토스 정산 DATA 		
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @RETURN_CODE INT = 0									-- 리턴코드(저장완료)
	DECLARE @RETURN_MESSAGE NVARCHAR(MAX) = DBO.GET_ERR_MSG('0')	-- 리턴메시지


	BEGIN TRAN
	BEGIN TRY 			
						
		DECLARE @PA_TOSS_DEPOSIT TABLE(
			SALES_DATE			NVARCHAR(10),
			PAYMENT_DATE		NVARCHAR(10),
			PYMNT_MTHD			NVARCHAR(15),
			ORD_NO				NVARCHAR(150),
			PAYAMT_AMT			NUMERIC(13,2),
			FEE					NUMERIC(13,2)
		);
		
		DELETE PA_TOSS_DEPOSIT WHERE SOLDATE = @P_DEPOSIT_DT
		

		INSERT INTO @PA_TOSS_DEPOSIT	
		SELECT 
			SALES_DATE,
			PAYMENT_DATE, 			
			PYMNT_MTHD,
			ORD_NO,			
			PAYAMT_AMT, 
			FEE
		FROM 
			OPENJSON ( @P_JSONDT )   
				WITH (    					
					SALES_DATE			NVARCHAR(10) '$.SALES_DATE',	--매출일자
					PAYMENT_DATE		NVARCHAR(10) '$.PAYMENT_DATE',	--지급일자
					PYMNT_MTHD			NVARCHAR(15) '$.PYMNT_MTHD',	--결제수단 SC0010 : 신용카드, SC0040 : 무통장입금(가상계좌), SC0030 : 계좌이체
					ORD_NO				NVARCHAR(150) '$.ORD_NO',		--주문번호
					PAYAMT_AMT			NUMERIC(13,2) '$.PAYAMT_AMT',	--거래금액
					FEE					NUMERIC(13,2) '$.FEE'			--수수료	
				) AS T1
		

		INSERT INTO PA_TOSS_DEPOSIT (SOLDATE, PAIDOUTDATE, METHOD, ACQUIRERCODE, PAYMENTKEY, ORDERID, AMOUNT, FEE, VAT, SUPPLYAMOUNT, PAYOUTAMOUNT, IDATE)
		SELECT
			SALES_DATE AS SOLDATE,
			PAYMENT_DATE AS PAIDOUTDATE, 			
			(CASE WHEN PYMNT_MTHD = 'SC0010' THEN '카드' WHEN PYMNT_MTHD = 'SC0040' THEN '가상계좌' WHEN PYMNT_MTHD = 'SC0030' THEN '계좌이체' END) AS METHOD,
			'' AS ACQUIRERCODE,
			'' AS PAYMENTKEY,
			ORD_NO AS ORDERID,			
			PAYAMT_AMT AS AMOUNT, 
			FEE,
			0 AS VAT,
			0 AS SUPPLYAMOUNT,
			(PAYAMT_AMT - FEE) AS PAYOUTAMOUNT,
			GETDATE()
		FROM @PA_TOSS_DEPOSIT
						
		SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE

		COMMIT;

	END TRY
	BEGIN CATCH	
				
		IF @@TRANCOUNT > 0
		BEGIN 
			ROLLBACK TRAN
			
			--에러 로그 테이블 저장
			INSERT INTO TBL_ERROR_LOG 
			SELECT ERROR_PROCEDURE()			-- 프로시저명
					, ERROR_MESSAGE()			-- 에러메시지
					, ERROR_LINE()				-- 에러라인
					, GETDATE()	

			
			SELECT -1 AS RETURN_CODE, ERROR_MESSAGE() AS RETURN_MESSAGE

		END 
	END CATCH
	
END

GO

