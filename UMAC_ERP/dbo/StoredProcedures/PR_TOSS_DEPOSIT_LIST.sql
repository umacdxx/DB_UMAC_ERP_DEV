/*

-- 생성자 :	이동호
-- 등록일 :	2024.06.28

-- 수정자 : 2024.12.12 이동호 일반계좌 조회 추가
			2024.12.18 최수민 - 토스(가상계좌, 카드) 그룹으로 수정, 매입사 : 토스-토스 / 일반계좌-GENERAL_ACC_NO_LIST
-- 설 명  : 토스입금 정산 및 일반계좌 입금 데이터 출력
-- 실행문 : EXEC PR_TOSS_DEPOSIT_LIST '20241206'

*/
CREATE PROCEDURE [dbo].[PR_TOSS_DEPOSIT_LIST]
( 	
	@P_DEPOSIT_DT		NVARCHAR(8)		-- 입금일자	
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @RETURN_CODE INT = 0									-- 리턴코드(저장완료)
	DECLARE @RETURN_MESSAGE NVARCHAR(MAX) = DBO.GET_ERR_MSG('0')	-- 리턴메시지

	BEGIN TRY 			
		
		DECLARE @TOSS_ACCT_NO			NVARCHAR(20) = '',
				@TOSS_ACCT_NAME			NVARCHAR(50) = '';
		SELECT @TOSS_ACCT_NO = CD_NM, @TOSS_ACCT_NAME = CD_DESCRIPTION FROM TBL_COMM_CD_MST WHERE CD_CL = 'GENERAL_ACC_NO_LIST' AND MGMT_ENTRY_1 = 'TOSS';

		--토스
		SELECT TOSS.PAIDOUTDATE
			 , 'TOSS' AS VEN_CODE
			 , '토스페이먼츠' AS VEN_NAME
			 , '토스' AS METHOD
			 , @TOSS_ACCT_NO AS ACCT_NO
			 , CONCAT(@TOSS_ACCT_NAME, ' (', @TOSS_ACCT_NO, ')') AS ACCT_NAME
			 , SUM(TOSS.AMOUNT) AS AMOUNT
			 , SUM(TOSS.FEE) AS FEE
			 , SUM(TOSS.PAYOUTAMOUNT) AS PAYOUTAMOUNT
		  FROM PA_TOSS_DEPOSIT AS TOSS
		 WHERE TOSS.PAIDOUTDATE = @P_DEPOSIT_DT
		 GROUP BY TOSS.PAIDOUTDATE

		UNION ALL

		--일반계좌
		SELECT DEPOSIT_DT AS PAIDOUTDATE
			 , ACCT.VEN_CODE
			 , PTN.VEN_NAME
			 , '일반계좌' AS METHOD
			 , PTCL.CRYP_CMSV_ACCT_NO AS ACCT_NO
			 , CONCAT(COM.CD_DESCRIPTION, ' (', PTCL.CRYP_CMSV_ACCT_NO, ')') AS ACCT_NAME
			 , SUM(DEPOSIT_AMT) AS AMOUNT
			 , 0 AS FEE
			 , SUM(DEPOSIT_AMT) AS PAYOUTAMOUNT	
		  FROM PA_ACCT_DEPOSIT AS ACCT
		 INNER JOIN HCMS_ACCT_TRSC_PTCL AS PTCL ON ACCT.MOID = PTCL.MOID
													AND PTCL.RCV_WDRW_DV_CD = '1'
													AND PTCL.CRYP_CMSV_ACCT_NO IN (SELECT CD_NM FROM TBL_COMM_CD_MST WHERE CD_CL = 'GENERAL_ACC_NO_LIST')
		 INNER JOIN TBL_COMM_CD_MST AS COM ON CD_CL = 'GENERAL_ACC_NO_LIST' AND COM.CD_NM = PTCL.CRYP_CMSV_ACCT_NO
		 INNER JOIN CD_PARTNER_MST AS PTN ON ACCT.VEN_CODE = PTN.VEN_CODE
		 WHERE DEPOSIT_GB = '02'
		   AND DEPOSIT_DT = @P_DEPOSIT_DT
		 GROUP BY DEPOSIT_DT, ACCT.VEN_CODE, PTN.VEN_NAME, COM.CD_DESCRIPTION, PTCL.CRYP_CMSV_ACCT_NO
		 ORDER BY VEN_CODE

	END TRY
	BEGIN CATCH	
				
				
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()			-- 프로시저명
				, ERROR_MESSAGE()			-- 에러메시지
				, ERROR_LINE()				-- 에러라인
				, GETDATE()	

			
		SELECT -1 AS RETURN_CODE, ERROR_MESSAGE() AS RETURN_MESSAGE

		
	END CATCH
	
END

GO

