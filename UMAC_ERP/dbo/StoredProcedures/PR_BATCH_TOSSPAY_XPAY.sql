
/*
-- 생성자 :	이동호
-- 등록일 :	2024.07.12
-- 수정자 : -
-- 수정일 : - 
-- 설 명 : 
-- 실행문 : 토스 XPAY 카드 결제한 데이터 API로 호출 데이터 저장
EXEC PR_BATCH_TOSSPAY_XPAY 'http://erp.umac.co.kr:8081', 'TRX'
*/
CREATE PROCEDURE [dbo].[PR_BATCH_TOSSPAY_XPAY]
	@P_URL NVARCHAR(150),		
	@P_SERVICECODE NVARCHAR(3)	--TRX : 거래건, REV : 취소건
AS
BEGIN


SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @RETURN_CODE INT = 0				-- 리턴코드
	DECLARE @RETURN_MESSAGE NVARCHAR(10) = ''	-- 리턴메시지
	DECLARE @V_START_TIME DATETIME = GETDATE();
	DECLARE @IDATE VARCHAR(8) = FORMAT(GETDATE(), 'yyyyMMdd')
	DECLARE @P_TYPE				NVARCHAR(10) = 'GET'	
	DECLARE @URL				NVARCHAR(250) = @P_URL + '/Api/TossPay/Transactions_Card?searchDate='+@IDATE+'&servicecode=' + @P_SERVICECODE
	DECLARE @P_BODY				NVARCHAR(4000) = ''
	DECLARE @R_RETURN_CODE 		INT 		
	DECLARE @R_RETURN_DATA 		VARCHAR(MAX) 

	BEGIN TRAN
	BEGIN TRY 

		EXEC SP_SEND_API @P_TYPE, @URL, @P_BODY, @R_RETURN_CODE OUT, @R_RETURN_DATA OUT

		IF @R_RETURN_DATA != ''
		BEGIN

			;WITH TMP_TBL AS (
				SELECT MID, PAYTYPE, [TRANSACTION], OID, FINANCECODE, FINANCENAME, BUYERID, AMOUNT, USEESCROW, PAYDATE, AUTHNMUNBER, [STATUS] FROM 
					OPENJSON ( @R_RETURN_DATA )   
						WITH (    
							MID				NVARCHAR(15) '$.mid',
							PAYTYPE			CHAR(6) '$.paytype',
							[TRANSACTION]	NVARCHAR(24) '$.transaction',
							OID				NVARCHAR(64) '$.oid',
							FINANCECODE		NVARCHAR(10) '$.financecode',
							FINANCENAME		NVARCHAR(10) '$.financename',
							BUYERID			NVARCHAR(15) '$.buyerid',
							AMOUNT			INT '$.amount',
							USEESCROW		CHAR(1) '$.useescrow',
							PAYDATE			NVARCHAR(8) '$.paydate',
							AUTHNMUNBER		NVARCHAR(10) '$.authnumber',
							[STATUS]		CHAR(2) '$.status'	--10: 거래 성공, 11: 거래 실패, 20: 취소/환불성공, 21: 취소/환불실패	
						) 
			)
		
			MERGE TBL_TOSSPAY_XPAY AS A
			USING (	
					
				SELECT * FROM TMP_TBL
				
			) AS B ON ( 
						A.OID  = B.OID AND A.[STATUS] = B.[STATUS]
			)		
			WHEN NOT MATCHED THEN
				INSERT (MID, PAYTYPE, [TRANSACTION], OID, FINANCECODE, FINANCENAME, BUYERID, AMOUNT, USEESCROW, PAYDATE, AUTHNMUNBER, [STATUS])
					VALUES (B.MID, B.PAYTYPE, B.[TRANSACTION], B.OID, B.FINANCECODE, B.FINANCENAME, B.BUYERID, B.AMOUNT, B.USEESCROW, B.PAYDATE, B.AUTHNMUNBER, B.[STATUS]);

		END 
		COMMIT;

	END TRY
	BEGIN CATCH		

	    IF @@TRANCOUNT > 0
		BEGIN
			ROLLBACK TRAN;
			
			SET @RETURN_CODE = -1
			SET @RETURN_MESSAGE = ERROR_MESSAGE()

			INSERT INTO TBL_BATCH_LOG
			SELECT CONVERT(VARCHAR(8), GETDATE(), 112)
				, 'PR_BATCH_TOSSPAY_XPAY'
				, 'F'
				, @V_START_TIME
				, GETDATE()
				, CONCAT(ERROR_MESSAGE(), ' / ', @RETURN_CODE)
				, 'N'
			;
					
		END;
		
	END CATCH

	SELECT @RETURN_CODE AS RETURN_CODE, @RETURN_MESSAGE AS RETURN_MESSAGE
END

GO

