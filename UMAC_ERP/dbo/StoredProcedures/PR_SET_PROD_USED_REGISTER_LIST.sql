/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.08.13
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : SET 작업 조회, 자재 사용 현황
-- 실행문 : EXEC PR_SET_PROD_USED_REGISTER_LIST '','',''
*/
CREATE PROCEDURE [dbo].[PR_SET_PROD_USED_REGISTER_LIST]
( 
	@P_SET_PLAN_ID			NVARCHAR(11) = '',		-- 작업코드
	@P_SET_PROD_SDATE		NVARCHAR(8) = '',		-- 생산시작일자
	@P_SET_PROD_EDATE		NVARCHAR(8) = ''		-- 생산종료일자
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	
	
		WITH W_MAIN AS (
			SELECT A.PROD_DT
                 , MAX(A.SCAN_CODE) AS UP_SCAN_CODE
				 , B.SET_COMP_CD AS SCAN_CODE
				 , SUM(A.PROD_QTY * B.COMP_QTY) AS USED_QTY
				 , MAX(A.IEMP_ID) AS IEMP_ID
				FROM PD_SET_RESULT_PROD AS A
				INNER JOIN (
					SELECT A.SET_PROD_CD AS SCAN_CODE
						 , B.SET_COMP_CD
						 , B.COMP_QTY
						FROM CD_SET_HDR AS A
						INNER JOIN CD_SET_DTL AS B ON A.SET_CD = B.SET_CD
						--GROUP BY A.SET_PROD_CD, B.SET_COMP_CD		-- SET마스터에서 SCAN_CODE 중복으로 들어가는 것 없어졌기 때문에 그루핑 해제
				) AS B ON A.SCAN_CODE = B.SCAN_CODE
				WHERE A.SET_PLAN_ID = @P_SET_PLAN_ID
				  AND A.PROD_DT BETWEEN @P_SET_PROD_SDATE AND @P_SET_PROD_EDATE
				GROUP BY A.PROD_DT, B.SET_COMP_CD
		)
		SELECT A.PROD_DT
			 , A.SCAN_CODE
			 , MST_PRD.ITM_NAME_DETAIL AS ITM_NAME
			 , CAST(A.USED_QTY AS INT) AS USED_QTY
			 , MST_USR.USER_NM
			FROM W_MAIN AS A
			INNER JOIN CD_PRODUCT_CMN AS MST_PRD ON A.SCAN_CODE = MST_PRD.SCAN_CODE
			INNER JOIN TBL_USER_MST AS MST_USR ON A.IEMP_ID = MST_USR.USER_ID
		   WHERE A.USED_QTY != 0
		   ORDER BY A.PROD_DT, MST_PRD.ITM_GB, A.UP_SCAN_CODE, A.SCAN_CODE

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
END

GO

