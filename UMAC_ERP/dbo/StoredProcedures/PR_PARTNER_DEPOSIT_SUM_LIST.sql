/*
-- 생성자 :	윤현빈
-- 등록일 :	2024.06.19
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : 거래처 별 입금내역 조회 (합계금액)
-- 실행문 : EXEC PR_PARTNER_DEPOSIT_SUM_LIST '','',''
*/
CREATE PROCEDURE [dbo].[PR_PARTNER_DEPOSIT_SUM_LIST]
( 
	@P_VEN_CODE		NVARCHAR(7) = '',	-- 거래처코드
	@P_FROM_DT		NVARCHAR(8) = '',	-- FROM 일자
	@P_TO_DT		NVARCHAR(8) = ''	-- TO 일자
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
	BEGIN TRY 	

		SELECT A.VEN_CODE
			 , B.VEN_NAME
			 , A.DEPOSIT_AMT
			 , @P_FROM_DT AS FROM_DT
			 , @P_TO_DT AS TO_DT
			FROM (
				SELECT A.VEN_CODE
					 , SUM(A.DEPOSIT_AMT) AS DEPOSIT_AMT
					FROM PA_ACCT_DEPOSIT AS A
				   WHERE A.DEPOSIT_DT BETWEEN @P_FROM_DT AND @P_TO_DT
					 AND 1=(CASE WHEN @P_VEN_CODE = '' THEN 1 WHEN @P_VEN_CODE != '' AND A.VEN_CODE = @P_VEN_CODE THEN 1 ELSE 2 END )
				   GROUP BY A.VEN_CODE
			) AS A
			INNER JOIN CD_PARTNER_MST AS B ON A.VEN_CODE = B.VEN_CODE
		   ORDER BY A.VEN_CODE

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

