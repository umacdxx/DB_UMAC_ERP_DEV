/*
-- 생성자 :	강세미
-- 등록일 :	2023.01.17
-- 수정자 : 2024.12.16 강세미- DIRECT_EXPORT_YN(직수출여부) 컬럼 추가
-- 수정일 : - 
-- 설 명  : 거래처 마스터 리스트 출력
-- 실행문 : 
EXEC PR_PARTNER_MST_LIST '동호', '', ''
*/
CREATE PROCEDURE [dbo].[PR_PARTNER_MST_LIST]
( 
	@P_VEN_CODE	NVARCHAR(50) = '',	-- 거래처명 및 코드
	@P_USE_YN		NVARCHAR(1) = '',	-- 사용여부
	@P_VEN_GB		NVARCHAR(10) = '' -- 거래처구분
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	SET @P_VEN_CODE = ISNULL(@P_VEN_CODE, '')
	SET @P_USE_YN = ISNULL(@P_USE_YN, '')
	SET @P_VEN_GB = ISNULL(@P_VEN_GB, '')
	 
	BEGIN TRY 
	EXEC UMAC_CERT_OPEN_KEY; -- OPEN

		SELECT A.VEN_CODE
			  , A.VEN_NAME
			  , A.VEN_GB
			  , B.CD_NM AS VEN_GB_NM
			  , A.REP_NAME
			  , A.POST_NO
			  , A.ADDR
			  , A.ADDR_DTL
			  , A.ADDR + ' ' + A.ADDR_DTL AS FULL_ADDR
			  , A.UPJONG
			  , A.UPTAE
			  , A.BUSI_NO
			  , A.TEL_NO
			  , A.FAX_NO
			  , A.REP_MAIL_ID
			  , A.PARTNER_CD
			  , A.BANK_CODE
			  , C.CD_NM AS BANK_NAME
			  --, A.BANK_ACC_NO
			  , DBO.GET_DECRYPT(A.BANK_ACC_NO) AS BANK_ACC_NO
			  , A.BANK_ACC_OWN
			  , A.BANK_PAY_CON			  
			  , A.CREDIT_LIMIT_YN
			  , ISNULL(A.CREDIT_LIMIT,0) AS CREDIT_LIMIT
			  , A.MGNT_DEPT
			  , A.MGNT_USER_ID
			  , D.USER_NM AS MGNT_USER_NM
			  , A.USE_YN
			  , ISNULL(A.CREDIT_LIMIT - (ISNULL(E.SALE_TOTAL_AMT,0) - ISNULL(F.DEPOSIT_AMT, 0)),0) AS CREDIT_BALANCE
			  , A.DIRECT_EXPORT_YN
		  FROM CD_PARTNER_MST AS A
				INNER JOIN TBL_COMM_CD_MST AS B
					ON B.CD_CL = 'VEN_GB' AND B.CD_ID = A.VEN_GB
				LEFT OUTER JOIN TBL_COMM_CD_MST AS C
					ON C.CD_CL = 'BANK_CODE' AND C.CD_ID = A.BANK_CODE
				LEFT OUTER JOIN TBL_USER_MST AS D 
					ON A.MGNT_USER_ID = D.[USER_ID]
				LEFT OUTER JOIN (SELECT A.VEN_CODE,
										SUM(A.SALE_TOTAL_AMT) AS SALE_TOTAL_AMT
								  FROM SL_SALE_VEN AS A
							  GROUP BY A.VEN_CODE 
								) AS E ON A.VEN_CODE = E.VEN_CODE
				LEFT OUTER JOIN (SELECT A.VEN_CODE,
									    SUM(A.DEPOSIT_AMT) AS DEPOSIT_AMT
								   FROM PA_ACCT_DEPOSIT AS A
								  WHERE DEPOSIT_FISH = 'Y'
							   GROUP BY A.VEN_CODE
								) AS F ON A.VEN_CODE = F.VEN_CODE


		 WHERE (A.VEN_CODE LIKE '%' + (CASE WHEN @P_VEN_CODE <> '' THEN @P_VEN_CODE ELSE A.VEN_CODE END) + '%' 
		    OR A.VEN_NAME LIKE '%' + (CASE WHEN @P_VEN_CODE <> '' THEN @P_VEN_CODE ELSE A.VEN_NAME END) + '%')
			AND A.USE_YN = (CASE WHEN @P_USE_YN <> '' THEN @P_USE_YN ELSE A.USE_YN END)
			AND A.VEN_GB = (CASE WHEN @P_VEN_GB <> '' THEN @P_VEN_GB ELSE A.VEN_GB END)

			
	EXEC UMAC_CERT_CLOSE_KEY -- CLOSE
	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

