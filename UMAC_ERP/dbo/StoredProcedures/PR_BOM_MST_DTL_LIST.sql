/*
-- 생성자 :	강세미
-- 등록일 :	2023.01.29
-- 수정자 : -
-- 수정일 : - 
-- 설 명  : BOM 마스터 DTL 리스트 출력
-- 실행문 : 
EXEC PR_BOM_MST_DTL_LIST '8801052951713','','','','Y'
EXEC PR_BOM_MST_DTL_LIST '','','','001'
-- 20240528 윤현빈 수정, WEIGHT_GB 추가
*/
CREATE PROCEDURE [dbo].[PR_BOM_MST_DTL_LIST]
( 
	@P_SCAN_CODE		NVARCHAR(14) = '',		-- 상품코드
	@P_LRG_CODE			NVARCHAR(2) = '',		-- 대분류코드
	@P_MID_CODE			NVARCHAR(4) = '',		-- 중분류코드
	@P_BOM_CD			NVARCHAR(4) = '',		-- BOM관리코드
	@P_BOM_CD_ALL		NVARCHAR(1) = 'N'		-- 상품에 포함된 구성품 전체 : Y, N
)
AS
BEGIN
SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	BEGIN TRY 
	IF ISNULL(@P_BOM_CD, '') = '' AND @P_BOM_CD_ALL = 'Y' 
	BEGIN
		
			SELECT ROW_NUMBER() OVER(ORDER BY BOM_COMP_CD) AS ROW_NUM
			  , A.BOM_PROD_CD AS ITM_CODE
			  , B.BOM_CD
			  , B.BOM_COMP_CD
			  , B.BOM_COMP_CD AS BOM_COMP_CD_BEFORE			  			  
			  , C.ITM_NAME_DETAIL AS BOM_COMP_NM
			  , C.WEIGHT_GB
			  , B.COMP_QTY			  
			  , D.CD_NM AS WEIGHT_GB_NM
			FROM CD_BOM_HDR AS A
				INNER JOIN CD_BOM_DTL AS B 
					ON A.BOM_CD = B.BOM_CD
				INNER JOIN CD_PRODUCT_CMN AS C
					ON B.BOM_COMP_CD = C.SCAN_CODE
				INNER JOIN TBL_COMM_CD_MST AS D
					ON D.CD_CL = 'WEIGHT_GB' AND C.WEIGHT_GB = D.CD_ID
			WHERE A.BOM_PROD_CD = @P_SCAN_CODE
			ORDER BY B.BOM_CD ASC, B.BOM_COMP_CD ASC

		RETURN
	END

	-- 조회조건에서 선택한 구성품 조회
	IF ISNULL(@P_BOM_CD, '') = ''
		BEGIN	
			
			SELECT 
			ROW_NUM,
			BOM_COMP_CD,
			BOM_COMP_CD_BEFORE,
			T.ITM_CODE,
			BOM_CD,
			COMP_QTY,
			T.WEIGHT_GB,
			WEIGHT_GB_NM,
			CM.ITM_NAME_DETAIL AS BOM_COMP_NM
			FROM (
				SELECT ROW_NUMBER() OVER(ORDER BY BOM_COMP_CD) AS ROW_NUM
				  , A.BOM_COMP_CD
				  , A.BOM_COMP_CD AS BOM_COMP_CD_BEFORE			  
				  , B.ITM_CODE			  
				  , '' AS BOM_CD
				  , 0.00 AS COMP_QTY
				  , B.WEIGHT_GB
				  , E.CD_NM AS WEIGHT_GB_NM
				 FROM CD_BOM_DTL AS A
					INNER JOIN CD_PRODUCT_CMN AS B
						ON B.SCAN_CODE = A.BOM_COMP_CD
					INNER JOIN CD_MID_MST AS C
						ON C.MID_CODE = B.MID_CODE
					INNER JOIN CD_LRG_MST AS D
						ON D.LRG_CODE = C.LRG_CODE
					INNER JOIN TBL_COMM_CD_MST AS E 
						ON E.CD_CL = 'WEIGHT_GB' AND B.WEIGHT_GB = E.CD_ID
		 		WHERE 1 = (CASE WHEN @P_SCAN_CODE = '' THEN 1 WHEN @P_SCAN_CODE <> '' AND B.SCAN_CODE = @P_SCAN_CODE THEN 1 ELSE 0 END)
					AND (D.LRG_CODE = (CASE WHEN @P_LRG_CODE <> '' THEN @P_LRG_CODE ELSE D.LRG_CODE END) AND C.MID_CODE = (CASE WHEN @P_MID_CODE <> '' THEN @P_MID_CODE ELSE C.MID_CODE END))										
				GROUP BY A.BOM_COMP_CD, ITM_CODE, B.WEIGHT_GB, E.CD_NM
			) AS T INNER JOIN CD_PRODUCT_CMN AS CM ON T.BOM_COMP_CD = CM.SCAN_CODE
			
		END
	ELSE 
	-- HDR 그리드 클릭 시 조회
		
		BEGIN
			SELECT ROW_NUMBER() OVER(ORDER BY BOM_CD) AS ROW_NUM
			  , A.BOM_CD
			  , A.BOM_COMP_CD
			  , A.BOM_COMP_CD AS BOM_COMP_CD_BEFORE			 			  
			  , A.COMP_QTY
			  , B.ITM_CODE
			  , B.ITM_NAME_DETAIL AS BOM_COMP_NM
			  , '' AS BOM_GB
			  , B.WEIGHT_GB
			  , C.CD_NM AS WEIGHT_GB_NM
			 FROM CD_BOM_DTL AS A
				INNER JOIN CD_PRODUCT_CMN AS B
					ON B.SCAN_CODE = A.BOM_COMP_CD
				INNER JOIN TBL_COMM_CD_MST AS C
					ON C.CD_CL = 'WEIGHT_GB' AND B.WEIGHT_GB = C.CD_ID
		 	WHERE A.BOM_CD = @P_BOM_CD
		END

	END TRY
	
	BEGIN CATCH		
		--에러 로그 테이블 저장
		INSERT INTO TBL_ERROR_LOG 
		SELECT ERROR_PROCEDURE()	-- 프로시저명
		, ERROR_MESSAGE()			-- 에러메시지
		, ERROR_LINE()				-- 에러라인
		, GETDATE()	
	END CATCH
	
END

GO

